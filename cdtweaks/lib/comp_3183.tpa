/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Romance Cheats                                   \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

// make array for standard bg2 romances
ACTION_DEFINE_ASSOCIATIVE_ARRAY cd_romances BEGIN
  Aerie   => aeri25
  Anomen  => anom25
  Jaheira => jahe25
  Viconia => vico25
END

ACTION_IF enhanced_edition THEN BEGIN

  // add bg2ee npcs
  ACTION_DEFINE_ASSOCIATIVE_ARRAY cd_romances BEGIN
    Dorn    => dorn25
    Hexxat  => hexxa25
    Neera   => neer25
    Rasaad  => rasa25
  END

END

// fixes for Anomen's join dialogue
COMPILE ~cdtweaks/dlg/ano_rom_fix.d~

// take care of tob_start
// this will also take care of any romance requirement adjustments on the ToB side while we're here
ACTION_IF romance_starts_in_ToB AND game_includes_tob THEN BEGIN

  EXTEND_TOP ~aeri25.bcs~ ~cdtweaks/baf/aeri25_start_tob.baf~

  ACTION_IF enhanced_edition THEN BEGIN
    COMPILE ~cdtweaks/dlg/rom_start_tob_bg2ee.d~ // order is different
  END ELSE BEGIN
    COMPILE ~cdtweaks/dlg/rom_start_tob.d~
  END

  ACTION_IF (remove_romance_gender_requirements != 1) THEN BEGIN // if gender reqs intact

    COMPILE ~cdtweaks/dlg/rom_start_tob_gender.d~

  END

  ACTION_IF (remove_romance_racial_requirements != 1) THEN BEGIN // if  race reqs intact

    COMPILE ~cdtweaks/dlg/rom_start_tob_race.d~

  END

  ACTION_IF (romance_multiple != 1) THEN BEGIN // if no multi allowed

    COMPILE ~cdtweaks/dlg/rom_start_tob_multi.d~

  END

END

ACTION_IF ((remove_romance_racial_requirements = 1) OR (remove_romance_gender_requirements = 1)) BEGIN

  ACTION_PHP_EACH cd_romances AS npc => tob BEGIN

    EXTEND_TOP ~%npc%.bcs~ ~cdtweaks/baf/%npc%_rom_reqs.baf~

  END

  ACTION_IF (remove_romance_racial_requirements != 1) BEGIN // if racial requirements intact (ee romances have no racial reqs)

    COPY_EXISTING ~aerie.bcs~ ~override~
      DECOMPILE_AND_PATCH BEGIN
        REPLACE_TEXTUALLY ~\(Global("CDRomanceFoo","LOCALS",0)\)~
        ~\1 OR(5) Race(Player1,HUMAN) Race(Player1,HALF_ELF) Race(Player1,ELF) Race(Player1,HALFLING) Race(Player1,GNOME)~
      END
      BUT_ONLY

    COPY_EXISTING ~anomen.bcs~  ~override~
                  ~jaheira.bcs~ ~override~
      DECOMPILE_AND_PATCH BEGIN
        REPLACE_TEXTUALLY ~\(Global("CDRomanceFoo","LOCALS",0)\)~
        ~\1 OR(4) Race(Player1,HUMAN) Race(Player1,HALF_ELF) Race(Player1,ELF) Race(Player1,HALFLING)~
      END
      BUT_ONLY

    COPY_EXISTING ~viconia.bcs~ ~override~
      DECOMPILE_AND_PATCH BEGIN
        REPLACE_TEXTUALLY ~\(Global("CDRomanceFoo","LOCALS",0)\)~
        ~\1 OR(4) Race(Player1,HUMAN) Race(Player1,HALF_ELF) Race(Player1,HALFLING) Race(Player1,HALFORC)~
      END
      BUT_ONLY

  END

  ACTION_IF (remove_romance_gender_requirements != 1) BEGIN // if gender requirements intact

    ACTION_FOR_EACH npc IN aerie jaheira neera viconia BEGIN

      ACTION_IF FILE_EXISTS_IN_GAME ~%npc%.bcs~ THEN BEGIN

        COPY_EXISTING ~%npc%.bcs~ ~override~
          DECOMPILE_AND_PATCH BEGIN
            REPLACE_TEXTUALLY ~\(Global("CDRomanceFoo","LOCALS",0)\)~
            ~\1 Gender(Player1,MALE)~
          END
          BUT_ONLY

      END

    END

    ACTION_FOR_EACH npc IN anomen hexxat rasaad BEGIN

      ACTION_IF FILE_EXISTS_IN_GAME ~%npc%.bcs~ THEN BEGIN

        COPY_EXISTING ~%npc%.bcs~ ~override~
          DECOMPILE_AND_PATCH BEGIN
            REPLACE_TEXTUALLY ~\(Global("CDRomanceFoo","LOCALS",0)\)~
            ~\1 Gender(Player1,FEMALE)~
          END
          BUT_ONLY

      END

    END

  END

  ACTION_PHP_EACH cd_romances AS npc => tob BEGIN // cleanup dummy var

    COPY_EXISTING ~%npc%.bcs~ ~override~
      DECOMPILE_AND_PATCH BEGIN
        REPLACE_TEXTUALLY ~Global("CDRomanceFoo","LOCALS",0)~ ~~
      END
      BUT_ONLY

  END

END

// allow multiple romances
ACTION_IF (romance_multiple = 1) THEN BEGIN

  COMPILE ~cdtweaks/dlg/romance_multi.d~ // remove LT sniping killing romances

  ACTION_PHP_EACH cd_romances AS npc => tob BEGIN

    COPY_EXISTING ~%npc%.bcs~ ~override~
      DECOMPILE_AND_PATCH BEGIN
        PATCH_PHP_EACH cd_romances AS npc2 => tob2 BEGIN
          PATCH_IF ("%npc%" STRING_COMPARE_CASE "%npc2%") BEGIN // don't self-match
            REPLACE_TEXTUALLY ~\b\(Global("%npc2%RomanceActive","GLOBAL",[12])\)~ ~False()~
          END
        END
        PATCH_IF (romance_nothing_kills = 1) BEGIN // nothing kills romance option
          APPEND_FILE ~cdtweaks/baf/%npc%_rom3.baf~
        END
      END
      BUT_ONLY

    ACTION_IF game_includes_tob THEN BEGIN

      COPY_EXISTING ~%tob%.bcs~ ~override~
        DECOMPILE_AND_PATCH BEGIN
          PATCH_PHP_EACH cd_romances AS npc2 => tob2 BEGIN
            PATCH_IF ("%npc%" STRING_COMPARE_CASE "%npc2%") BEGIN // don't self-match
              REPLACE_TEXTUALLY ~\b\(Global("%npc2%RomanceActive","GLOBAL",[12])\)~ ~False()~
            END
          END
          REPLACE_TEXTUALLY ~GlobalGT("WraithPunish","GLOBAL",0)~ ~False()~
          APPEND_FILE ~cdtweaks/baf/%tob%_rom2.baf~
          PATCH_IF (romance_nothing_kills = 1) BEGIN // nothing kills romance option
            APPEND_FILE ~cdtweaks/baf/%tob%_rom3.baf~
          END
        END
        BUT_ONLY

    END

  END

END
