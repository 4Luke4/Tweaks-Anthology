DEFINE_ACTION_FUNCTION "SNEAKATT_BLACKGUARD"
BEGIN
	CREATE "eff" "cdblkgsa"
	COPY_EXISTING "cdblkgsa.eff" "override"
		WRITE_LONG 0x10 402 // invoke lua
		WRITE_SHORT 0x2C 100 // prob1
		WRITE_ASCII 0x30 "GTBLKG01" #8 // lua function
	BUT_ONLY
	//
	CREATE "spl" "cdblkgsa"
	COPY_EXISTING "cdblkgsa.spl" "override"
		WRITE_LONG NAME1 RESOLVE_STR_REF (@0)
		WRITE_LONG 0x18 BIT14 // flags: ignore dead/wild magic
		WRITE_SHORT 0x1C 4 // type: innate
		WRITE_LONG 0x34 1 // level
		//
		LPF "ADD_SPELL_HEADER" INT_VAR "range" = 30 END
		//
		LPF "ADD_SPELL_EFFECT" INT_VAR "opcode" = 324 "target" = 2 "parameter1" = IDS_OF_SYMBOL ("kit" "barbarian") "parameter2" = 109 STR_VAR "resource" = "%DEST_RES%" END // Immunity to resource and message
		PATCH_WITH_SCOPE BEGIN
			LPF "ADD_SPLPROT_ENTRY" INT_VAR "stat" = IDS_OF_SYMBOL ("STATS" "IMMUNITY_TO_BACKSTAB") STR_VAR "value" = "-1" "relation" = "4" "label" = "STAT(IMMUNITY_TO_BACKSTAB) >= n" RET "index" END
			LPF "ADD_SPELL_EFFECT" INT_VAR "opcode" = 324 "target" = 2 "parameter1" = 1 "parameter2" = "%index%" STR_VAR "resource" = "%DEST_RES%" END // Immunity to resource and message (IMMUNITY_TO_BACKSTAB >= 1)
		END
		LPF "ADD_SPELL_EFFECT" INT_VAR "opcode" = 324 "target" = 2 "parameter1" = IDS_OF_SYMBOL ("general" "plant") "parameter2" = 103 STR_VAR "resource" = "%DEST_RES%" END // Immunity to resource and message
		LPF "ADD_SPELL_EFFECT" INT_VAR "opcode" = 324 "target" = 2 "parameter2" = 55 STR_VAR "resource" = "%DEST_RES%" END // Immunity to resource and message (RACE=GOLEM || GENERAL=UNDEAD)
		LPF "ADD_SPELL_EFFECT" INT_VAR "opcode" = 324 "target" = 2 "parameter1" = IDS_OF_SYMBOL ("general" "weapon") "parameter2" = 103 STR_VAR "resource" = "%DEST_RES%" END // Immunity to resource and message (Animated weapons such as the Mordenkainen's Sword)
		PATCH_WITH_SCOPE BEGIN
			PATCH_FOR_EACH "race" IN "mist" "dragon" "beholder" "slime" "demonic" "mephit" "imp" "elemental" "salamander" "genie" "solar" "antisolar" "planatar" "darkplanatar" BEGIN
				LPF "ADD_SPELL_EFFECT" INT_VAR "opcode" = 324 "target" = 2 "parameter1" = IDS_OF_SYMBOL ("race" "%race%") "parameter2" = 104 STR_VAR "resource" = "%DEST_RES%" END // Immunity to resource and message
			END
		END
		//
		LPF "ADD_SPELL_EFFECT" INT_VAR "opcode" = 402 "target" = 2 STR_VAR "resource" = "GTBLKG02" END // invoke lua
	BUT_ONLY_IF_IT_CHANGES
	// Listener: run 'func' each time a sprite has finished evaluating its effects
	LAF "APPEND_LUA_FUNCTION" STR_VAR "description" = "Listeners" "sourceFileSpec" = "cdtweaks\luke\lua\sneakatt_blackguard_grant.lua" "destRes" = "m_gtlstn" END
	//
	LAF "APPEND_LUA_FUNCTION" STR_VAR "description" = "Functions to be invoked via op402" "sourceFileSpec" = "cdtweaks\luke\lua\sneakatt_blackguard.lua" "destRes" = "m_gt#402" END
	//
	LAF "APPEND_LUA_FUNCTION" STR_VAR "description" = "Utility Functions / Listeners" "sourceFileSpec" = "cdtweaks\luke\lua\utility\is_invisible.lua" "destRes" = "m_gtutil" END
	//
	ACTION_IF !(FILE_EXISTS_IN_GAME "m_gttbls.lua") BEGIN
		COPY "cdtweaks\luke\lua\m_gttbls.lua" "override"
	END
END