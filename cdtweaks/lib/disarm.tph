DEFINE_ACTION_FUNCTION "DISARM"
BEGIN
	LAF "GT_ADD_SPELL"
	INT_VAR
		"level" = 4
		"preferredSlot" = 44
		"type" = 4
	STR_VAR
		"idsName" = "ROGUE_DISARM"
	RET
		"ROGUE_DISARM" = "resName"
	END
	//
	//LAF "ADD_EXTENDED_STAT" INT_VAR "max" = 25 STR_VAR "identifier" = "GT_IGNORE_ACTION_ADD_SPRITE_STARTED_ACTION_LISTENER" END
	//
	WITH_SCOPE BEGIN
		ACTION_TO_LOWER "ROGUE_DISARM"
		// Disarm (main spl file)
		CREATE "spl" "%ROGUE_DISARM%"
		COPY_EXISTING "%ROGUE_DISARM%.spl" "override"
			WRITE_LONG NAME1 RESOLVE_STR_REF (@0)
			WRITE_LONG NAME2 "-1"
			WRITE_LONG UNIDENTIFIED_DESC RESOLVE_STR_REF (@1)
			WRITE_LONG DESC "-1"
			WRITE_LONG 0x18 (BIT14 BOR BIT25) // ignore dead/wild magic, castable when silenced
			WRITE_SHORT 0x1C 4 // innate
			WRITE_LONG 0x34 1 // level
			WRITE_ASCII 0x3A "%DEST_RES%B" #8 // icon
			//
			LPF "ADD_SPELL_HEADER" INT_VAR "range" = 30 STR_VAR "icon" = "%DEST_RES%B" END
			//
			/*LPF "ADD_SPELL_EFFECT" INT_VAR "opcode" = 138 "target" = 1 "parameter2" = 7 END // SEQ_READY
			LPF "ADD_SPELL_EFFECT" INT_VAR "opcode" = 321 "target" = 1 STR_VAR "resource" = "%DEST_RES%" END // Remove effects by resource
			LPF "ADD_SPELL_EFFECT" INT_VAR "opcode" = 402 "target" = 2 STR_VAR "resource" = "%DEST_RES%" END // Invoke lua*/
		BUT_ONLY
		// EFF
		CREATE "eff" "%ROGUE_DISARM%b"
		COPY_EXISTING "%ROGUE_DISARM%b.eff" "override"
			WRITE_LONG 0x10 402 // Invoke Lua
			WRITE_LONG 0x14 2 // Projectile target
			//WRITE_LONG 0x1C 1 // p1
			WRITE_SHORT 0x2C 100 // prob1
			WRITE_ASCII 0x30 "%ROGUE_DISARM%" #8 // Lua function
		BUT_ONLY
		// icon
		COPY "cdtweaks\luke\bam\class\disarm\spl_icon.bam" "override\%ROGUE_DISARM%b.bam"
	END
	// lua
	WITH_SCOPE BEGIN
		OUTER_SET "feedback_strref_inventory_full" = RESOLVE_STR_REF (@101)
		OUTER_SET "feedback_strref_resisted" = RESOLVE_STR_REF (@102)
		OUTER_SET "feedback_strref_hit" = RESOLVE_STR_REF (@103)
		OUTER_SET "feedback_strref_melee_only" = RESOLVE_STR_REF (@104)
		OUTER_SET "feedback_strref_aura_free" = RESOLVE_STR_REF (@105)
		OUTER_SET "feedback_strref_immune" = RESOLVE_STR_REF (@106)
		//
		LAF "ADD_STATDESC_ENTRY" INT_VAR "description" = RESOLVE_STR_REF (@0) STR_VAR "bam_file" = "%ROGUE_DISARM%D" RET "feedback_icon_can_disarm" = "index" END
		WITH_SCOPE BEGIN
			ACTION_TO_LOWER "ROGUE_DISARM"
			COPY "cdtweaks\luke\bam\class\disarm\portrait_icon.bam" "override\%ROGUE_DISARM%d.bam"
		END
		//
		LAF "APPEND_LUA_FUNCTION" STR_VAR "description" = "Class/Kit Abilities" "sourceFileSpec" = "cdtweaks\luke\lua\class\disarm.lua" "destRes" = "m_gtspcl" END
	END
	//
	//LAF "APPEND_LUA_FUNCTION" STR_VAR "description" = "Lua Tools" "sourceFileSpec" = "cdtweaks\luke\lua\tools\key_exists.lua" "destRes" = "m_gttool" END
	LAF "APPEND_LUA_FUNCTION" STR_VAR "description" = "Utility Functions / Listeners" "sourceFileSpec" = "cdtweaks\luke\lua\utility\effect_check.lua" "destRes" = "m_gtutil" END
	//
	ACTION_IF !(FILE_EXISTS_IN_GAME "m_gttbls.lua") BEGIN
		COPY "cdtweaks\luke\lua\m_gttbls.lua" "override"
	END
END