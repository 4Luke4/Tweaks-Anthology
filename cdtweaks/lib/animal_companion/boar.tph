/*
=====================================================================================================================
**CRE file**
=====================================================================================================================
*/

CREATE "cre" "gtacmp05"
COPY_EXISTING "gtacmp05.cre" "override"
	WRITE_LONG NAME1 RESOLVE_STR_REF (@5000) // name
	WRITE_LONG NAME2 RESOLVE_STR_REF (@5000) // tooltip
	//
	WRITE_LONG 0x10 BIT1 // flags: no corpse
	//
	WRITE_SHORT 0x24 1 // current HP
	WRITE_SHORT 0x26 1 // max HP
	//
	WRITE_LONG 0x28 IDS_OF_SYMBOL ("animate" "boar_wild")
	//
	WRITE_BYTE 0x2C 30 // metal color
	WRITE_BYTE 0x2D 37 // minor color
	WRITE_BYTE 0x2E 1 // major color
	WRITE_BYTE 0x2F 9 // skin color
	WRITE_BYTE 0x30 23 // leather color
	WRITE_BYTE 0x31 30 // armor color
	WRITE_BYTE 0x32 1 // hair color
	//
	WRITE_SHORT 0x46 7 // natural AC
	WRITE_SHORT 0x48 7 // effective AC
	//
	WRITE_BYTE 0x53 1 // # attacks/round
	//
	WRITE_BYTE 0x237 IDS_OF_SYMBOL ("gender" "female") // sex
	// abilities
	WRITE_BYTE 0x238 16 // STR
	WRITE_BYTE 0x23A 4 // INT
	WRITE_BYTE 0x23B 2 // WIS
	WRITE_BYTE 0x23C 10 // DEX
	WRITE_BYTE 0x23D 16 // CON
	WRITE_BYTE 0x23E 3 // CHA
	//
	WRITE_BYTE 0x23F 10 // morale
	WRITE_BYTE 0x240 5 // morale break
	WRITE_SHORT 0x242 60 // morale recovery
	//
	WRITE_ASCII SCRIPT_OVERRIDE "GTACMP00" #8
	WRITE_ASCII SCRIPT_CLASS "%DEST_RES%" #8
	WRITE_ASCII SCRIPT_DEFAULT "GTACMPAI" #8
	//
	WRITE_ASCII DEATHVAR "gtAnmlCompBoar" #32
	// IDS
	WRITE_BYTE 0x270 IDS_OF_SYMBOL ("ea" "neutral")
	WRITE_BYTE 0x271 IDS_OF_SYMBOL ("general" "animal")
	WRITE_BYTE 0x272 IDS_OF_SYMBOL ("race" "mammal")
	WRITE_BYTE 0x273 IDS_OF_SYMBOL ("class" "no_class")
	WRITE_BYTE 0x275 IDS_OF_SYMBOL ("gender" "female")
	WRITE_BYTE 0x27B IDS_OF_SYMBOL ("align" "neutral")
	// equipment
	ADD_CRE_ITEM "gtacp05a" #0 #0 #0 "identified&unstealable&undroppable" "weapon" EQUIP
	// passive traits
	LPF "ADD_CRE_EFFECT" INT_VAR "opcode" = 268 "target" = 1 "timing" = 1 END // clear fog of war
	LPF "ADD_CRE_EFFECT" INT_VAR "opcode" = 232 "target" = 1 "timing" = 1 "parameter2" = 16 "special" = BIT2 STR_VAR "resource" = "GTACMP00" END // cast spell onto myself upon death (suppress spell's name)
BUT_ONLY_IF_IT_CHANGES

/*
=====================================================================================================================
**ITM file(s)**
=====================================================================================================================
*/

CREATE "itm" "gtacp05a"
COPY_EXISTING "gtacp05a.itm" "override"
	/* Header */
	WRITE_LONG NAME1 "-1"
	WRITE_LONG NAME2 "-1"
	WRITE_LONG UNIDENTIFIED_DESC "-1"
	WRITE_LONG DESC "-1"
	WRITE_LONG 0x18 BIT5 // flags: not copyable (not needed...?)
	WRITE_SHORT 0x38 1 // maximum in stack
	WRITE_ASCII 0x3A "IWOLF" #8
	/* Extended Header */
	LPF ~ADD_ITEM_HEADER~ INT_VAR "type" = 1 "location" = 1 "range" = 1 "dicenumber" = 3 "dicesize" = 4 "damage_type" = 1 "depletion" = 1 "flags" = BIT0 "overhand" = 34 "backhand" = 33 "thrust" = 33 STR_VAR "icon" = "IWOLF" END // 3d4 (piercing)
BUT_ONLY_IF_IT_CHANGES
//
ACTION_DEFINE_ASSOCIATIVE_ARRAY "cdtweaksAnimalCompanion" BEGIN
	// id => enchantment
	"b" => 0 // magical (+0)
	"c" => 1 // magical (+1)
	"d" => 2 // magical (+2)
	"e" => 3 // magical (+3)
END
ACTION_PHP_EACH "cdtweaksAnimalCompanion" AS "k" => "v" BEGIN
	COPY_EXISTING "gtacp05a.itm" "override\gtacp05%k%.itm"
		WRITE_LONG 0x18 (THIS BOR BIT6) // flags: magical
		WRITE_LONG 0x60 "%v%" // enchantment
		LPF "ALTER_ITEM_HEADER" INT_VAR "damage_bonus" = "%v%" "thac0_bonus" = "%v%" END
		LPF "ADD_ITEM_EQEFFECT" INT_VAR "opcode" = 341 "target" = 1 "timing" = 2 "parameter2" = 1 STR_VAR "resource" = "GTACP05A" END // +5 extra Fury per critical hit
		LPF "ADD_ITEM_EFFECT" INT_VAR "type" = 1 "opcode" = 402 "target" = 1 STR_VAR "resource" = "GTACMP05" END // invoke lua: consume all fury to cause bleeding damage
		LPF "ADD_ITEM_EFFECT" INT_VAR "type" = 1 "opcode" = 146 "target" = 1 "timing" = 1 "parameter1" = 1 STR_VAR "resource" = "GTACP05A" END // +5 Fury per successful hit
		LPF "ADD_ITEM_EFFECT" INT_VAR "type" = 1 "opcode" = 326 "target" = 2 "timing" = 1 "parameter1" = IDS_OF_SYMBOL ("state" "state_dead") "parameter2" = 138 STR_VAR "resource" = "GTACP05A" END // +5 extra Fury per kill
	BUT_ONLY_IF_IT_CHANGES
END

/*
=====================================================================================================================
**SPL file(s)**
=====================================================================================================================
*/

CREATE "spl" "gtacp05a"
COPY_EXISTING "gtacp05a.spl" "override"
	/* Header */
	WRITE_LONG NAME1 "-1"
	WRITE_LONG NAME2 "-1"
	WRITE_LONG UNIDENTIFIED_DESC "-1"
	WRITE_LONG DESC "-1"
	WRITE_LONG 0x18 BIT14 // Ignore dead/wild magic
	WRITE_SHORT 0x1C 4 // Type: Innate
	WRITE_LONG 0x34 1 // Level
	/* Extended Header */
	LPF ~ADD_SPELL_HEADER~ INT_VAR "range" = 30 END
	/* Feature Blocks */
	LPF "ADD_SPELL_EFFECT" INT_VAR "opcode" = 177 "target" = 9 "timing" = 1 "parameter2" = 2 STR_VAR "resource" = "GTACP05A" END // Use EFF file
	LPF "ADD_SPELL_EFFECT" INT_VAR "opcode" = 9 "target" = 9 "parameter1" = (150 << 8) + (0 << 16) + (0 << 24) "parameter2" = 255 + (30 << 24) "duration" = 1 END // visual effect
BUT_ONLY
//
CREATE "eff" "gtacp05a"
COPY_EXISTING "gtacp05a.eff" "override"
	WRITE_LONG 0x10 309 // Set local int
	WRITE_SHORT 0x2C 100 // prob1
	WRITE_LONG 0x20 1 // increment
	WRITE_LONG 0x1C 5 // increment by 5
	WRITE_ASCII 0x30 "gtAnmlCo" #8
	WRITE_ASCII 0x70 "mpBoarFu" #8
	WRITE_ASCII 0x78 "ryAmount" #8
BUT_ONLY_IF_IT_CHANGES
//
CREATE "spl" "gtacp05b"
COPY_EXISTING "gtacp05b.spl" "override"
	/* Header */
	WRITE_LONG NAME1 RESOLVE_STR_REF (@5001)
	WRITE_LONG NAME2 "-1"
	WRITE_LONG UNIDENTIFIED_DESC "-1"
	WRITE_LONG DESC "-1"
	WRITE_LONG 0x18 BIT14 // Ignore dead/wild magic
	WRITE_SHORT 0x1C 4 // Type: Innate
	WRITE_LONG 0x34 1 // Level
	/* Extended Header */
	PATCH_WITH_SCOPE BEGIN
		FOR ("i" = 5 ; "%i%" <= 40 ; "i" += 5) BEGIN
			LPF ~ADD_SPELL_HEADER~ INT_VAR "range" = 30 "required_level" = ("%i%" == 5 ? 1 : "%i%") END
			/* Feature Blocks */
			LPF "ADD_SPELL_EFFECT" INT_VAR "header" = SHORT_AT 0x68 "opcode" = 324 "target" = 2 "parameter1" = IDS_OF_SYMBOL ("general" "plant") "parameter2" = 103 STR_VAR "resource" = "%DEST_RES%" END // Protection from resource
			LPF "ADD_SPELL_EFFECT" INT_VAR "header" = SHORT_AT 0x68 "opcode" = 324 "target" = 2 "parameter1" = IDS_OF_SYMBOL ("general" "weapon") "parameter2" = 103 STR_VAR "resource" = "%DEST_RES%" END // Protection from resource (Animated weapons such as the Mordenkainen's Sword)
			PATCH_WITH_SCOPE BEGIN
				PATCH_FOR_EACH "race" IN "demonic" "mephit" "imp" "elemental" "salamander" "genie" "solar" "antisolar" "planatar" "darkplanatar" BEGIN
					LPF "ADD_SPELL_EFFECT" INT_VAR "header" = SHORT_AT 0x68 "opcode" = 324 "target" = 2 "parameter1" = IDS_OF_SYMBOL ("race" "%race%") "parameter2" = 104 STR_VAR "resource" = "%DEST_RES%" END // Protection from resource (RACE=GOLEM || GENERAL=UNDEAD)
				END
			END
			LPF "ADD_SPELL_EFFECT" INT_VAR "header" = SHORT_AT 0x68 "opcode" = 12 "target" = 2 "timing" = 1 "parameter2" = IDS_OF_SYMBOL ("dmgtype" "piercing") "dicenumber" = 2 "dicesize" = 3 "savingthrow" = BIT2 "savebonus" = "-1" * (("%i%" / 5) - 1) "special" = BIT8 END // Damage (2d3, save vs. death for half)
			LPF "ADD_SPELL_EFFECT" INT_VAR "header" = SHORT_AT 0x68 "opcode" = 142 "target" = 2 "parameter2" = 137 "duration" = 30 "savingthrow" = BIT2 "savebonus" = "-1" * (("%i%" / 5) - 1) END // Bleeding icon
			LPF "ADD_SPELL_EFFECT" INT_VAR "header" = SHORT_AT 0x68 "opcode" = 139 "target" = 2 "timing" = 1 "parameter1" = RESOLVE_STR_REF (@5002) "savingthrow" = BIT2 "savebonus" = "-1" * (("%i%" / 5) - 1) END // "Suffers Bleeding Wound" feedback string
			PATCH_WITH_SCOPE BEGIN
				FOR ("i" = 6 ; "%i%" <= 30 ; "i" += 6) BEGIN
					LPF "CLONE_EFFECT" INT_VAR "multi_match" = 1 "header" = (SHORT_AT 0x68) - 1 "match_opcode" = 12 "timing" = 4 "duration" = "%i%" STR_VAR "insert" = "last" END
					LPF "CLONE_EFFECT" INT_VAR "multi_match" = 1 "header" = (SHORT_AT 0x68) - 1 "match_opcode" = 139 "timing" = 4 "duration" = "%i%" STR_VAR "insert" = "last" END
				END
			END
		END
	END
BUT_ONLY

/*
=====================================================================================================================
**LUA file(s)**
=====================================================================================================================
*/

COPY_EXISTING "m_gtlstn.lua" "override"
	APPEND_FILE TEXT "cdtweaks\luke\lua\animal_companion\boar\decrement_fury.lua"
BUT_ONLY UNLESS "gtAnmlCompBoarFuryAmount"
//
COPY_EXISTING "m_gt#402.lua" "override"
	APPEND_FILE TEXT "cdtweaks\luke\lua\animal_companion\boar\consume_fury.lua"
BUT_ONLY UNLESS "^function GTACMP05"