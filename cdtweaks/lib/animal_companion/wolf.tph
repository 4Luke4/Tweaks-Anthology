/*
=====================================================================================================================
**CRE file**
=====================================================================================================================
*/

CREATE "cre" "gtanml02"
COPY_EXISTING "gtanml02.cre" "override"
	WRITE_LONG NAME1 RESOLVE_STR_REF (@2000) // name
	WRITE_LONG NAME2 RESOLVE_STR_REF (@2000) // tooltip
	//
	WRITE_LONG 0x10 BIT1 // flags: no corpse
	//
	WRITE_SHORT 0x24 1 // current HP
	WRITE_SHORT 0x26 1 // max HP
	//
	WRITE_LONG 0x28 IDS_OF_SYMBOL ("animate" "wolf_winter")
	//
	WRITE_BYTE 0x2C 20 // metal color
	WRITE_BYTE 0x2D 31 // minor color
	WRITE_BYTE 0x2E 31 // major color
	WRITE_BYTE 0x2F 7 // skin color
	WRITE_BYTE 0x30 14 // leather color
	WRITE_BYTE 0x31 20 // armor color
	WRITE_BYTE 0x32 0 // hair color
	//
	WRITE_SHORT 0x46 5 // natural AC
	WRITE_SHORT 0x48 5 // effective AC
	//
	WRITE_BYTE 0x53 1 // # attacks/round
	// resistances
	WRITE_BYTE 0x59 "-50" // fire
	WRITE_BYTE 0x5A 100 // cold
	//
	WRITE_BYTE 0x237 IDS_OF_SYMBOL ("gender" "male") // sex
	// abilities
	WRITE_BYTE 0x238 18 // STR
	WRITE_BYTE 0x23A 10 // INT
	WRITE_BYTE 0x23B 8 // WIS
	WRITE_BYTE 0x23C 12 // DEX
	WRITE_BYTE 0x23D 16 // CON
	WRITE_BYTE 0x23E 6 // CHA
	//
	WRITE_BYTE 0x23F 13 // morale
	WRITE_BYTE 0x240 6 // morale break
	WRITE_SHORT 0x242 30 // morale recovery
	//
	WRITE_ASCII SCRIPT_OVERRIDE "GTACMP00" #8
	WRITE_ASCII SCRIPT_CLASS "%DEST_RES%" #8
	WRITE_ASCII SCRIPT_DEFAULT "GTACMPAI" #8
	//
	WRITE_ASCII DEATHVAR "gtAnmlCompWolf" #32
	// IDS
	WRITE_BYTE 0x270 IDS_OF_SYMBOL ("ea" "neutral")
	WRITE_BYTE 0x271 IDS_OF_SYMBOL ("general" "animal")
	WRITE_BYTE 0x272 IDS_OF_SYMBOL ("race" "wolf")
	WRITE_BYTE 0x273 IDS_OF_SYMBOL ("class" "wolf_winter")
	WRITE_BYTE 0x275 IDS_OF_SYMBOL ("gender" "male")
	WRITE_BYTE 0x27B IDS_OF_SYMBOL ("align" "neutral_evil")
	// equipment
	ADD_CRE_ITEM "gtacp02a" #0 #0 #0 "identified&unstealable&undroppable" "weapon" EQUIP
	// passive traits
	LPF "ADD_CRE_EFFECT" INT_VAR "opcode" = 268 "target" = 1 "timing" = 1 END // clear fog of war
	LPF "ADD_CRE_EFFECT" INT_VAR "opcode" = 232 "target" = 1 "timing" = 1 "parameter2" = 16 "special" = BIT2 STR_VAR "resource" = "GTACMP00" END // cast spell onto myself upon death (suppress spell's name)
	// spellbook
	ADD_MEMORIZED_SPELL "gtacmp00" #0 "innate"
BUT_ONLY_IF_IT_CHANGES

/*
=====================================================================================================================
**SPL file(s)**
=====================================================================================================================
*/

CREATE "spl" "gtacmp02"
COPY_EXISTING "gtacmp02.spl" "override"
	/* Header */
	WRITE_LONG NAME1 RESOLVE_STR_REF (@2001)
	WRITE_LONG NAME2 "-1"
	WRITE_LONG UNIDENTIFIED_DESC RESOLVE_STR_REF (@2002)
	WRITE_LONG DESC "-1"
	WRITE_LONG 0x18 (BIT14 BOR BIT25) // Ignore dead/wild magic, Castable when silenced
	WRITE_SHORT 0x1C 4 // Type: Innate
	WRITE_LONG 0x34 1 // Level
	WRITE_ASCII 0x3A ~%WIZARD_CONE_OF_COLD%C~ #8 // Icon
	/* Extended Header */
	PATCH_WITH_SCOPE BEGIN
		FOR ("i" = 5 ; "%i%" <= 40 ; "i" += 5) BEGIN
			LPF ~ADD_SPELL_HEADER~ INT_VAR "required_level" = ("%i%" == 5 ? 1 : "%i%") "range" = 30 "projectile" = IDS_OF_SYMBOL ("MISSILE" "New_Cone_Of_Cold") STR_VAR "icon" = ~%WIZARD_CONE_OF_COLD%B~ END
			/* Feature Blocks */
			LPF "ADD_SPELL_EFFECT" INT_VAR "header" = SHORT_AT 0x68 "opcode" = 12 "target" = 2 "timing" = 1 "parameter2" = IDS_OF_SYMBOL ("dmgtype" "cold") "dicenumber" = 6 "dicesize" = 4 "savingthrow" = BIT1 "savebonus" = "-1" * (("%i%" / 5) - 1) "special" = BIT8 END // Damage (6d4, save vs. breath for half)
		END
	END
BUT_ONLY

/*
=====================================================================================================================
**ITM file(s)**
=====================================================================================================================
*/

CREATE "itm" "gtacp02a"
COPY_EXISTING "gtacp02a.itm" "override"
	/* Header */
	WRITE_LONG NAME1 "-1"
	WRITE_LONG NAME2 "-1"
	WRITE_LONG UNIDENTIFIED_DESC "-1"
	WRITE_LONG DESC "-1"
	WRITE_LONG 0x18 BIT5 // flags: not copyable (not needed...?)
	WRITE_SHORT 0x38 1 // maximum in stack
	WRITE_ASCII 0x3A "IWOLF" #8
	/* Extended Header */
	LPF ~ADD_ITEM_HEADER~ INT_VAR "type" = 1 "location" = 1 "range" = 1 "dicenumber" = 2 "dicesize" = 4 "damage_type" = 1 "depletion" = 1 "flags" = BIT0 "overhand" = 34 "backhand" = 33 "thrust" = 33 STR_VAR "icon" = "IWOLF" END // 2d4 (piercing)
BUT_ONLY_IF_IT_CHANGES
//
ACTION_DEFINE_ASSOCIATIVE_ARRAY "cdtweaksAnimalCompanion" BEGIN
	// id => enchantment
	"b" => 0 // magical (+0)
	"c" => 1 // magical (+1)
	"d" => 2 // magical (+2)
	"e" => 3 // magical (+3)
END
ACTION_PHP_EACH "cdtweaksAnimalCompanion" AS "k" => "v" BEGIN
	COPY_EXISTING "gtacp02a.itm" "override\gtacp02%k%.itm"
		WRITE_LONG 0x18 (THIS BOR BIT6) // flags: magical
		WRITE_LONG 0x60 "%v%" // enchantment
		LPF "ALTER_ITEM_HEADER" INT_VAR "damage_bonus" = "%v%" "thac0_bonus" = "%v%" END
	BUT_ONLY_IF_IT_CHANGES
END