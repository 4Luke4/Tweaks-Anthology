/*
=====================================================================================================================
**CRE file**
=====================================================================================================================
*/

CREATE "cre" "gtacmp04"
COPY_EXISTING "gtacmp04.cre" "override"
	WRITE_LONG NAME1 RESOLVE_STR_REF (@4000) // name
	WRITE_LONG NAME2 RESOLVE_STR_REF (@4000) // tooltip
	//
	WRITE_LONG 0x10 BIT1 // flags: no corpse
	//
	WRITE_SHORT 0x24 1 // current HP
	WRITE_SHORT 0x26 1 // max HP
	//
	WRITE_LONG 0x28 IDS_OF_SYMBOL ("animate" "greatcat_leopard")
	//
	WRITE_BYTE 0x2C 20 // metal color
	WRITE_BYTE 0x2D 31 // minor color
	WRITE_BYTE 0x2E 31 // major color
	WRITE_BYTE 0x2F 7 // skin color
	WRITE_BYTE 0x30 14 // leather color
	WRITE_BYTE 0x31 20 // armor color
	WRITE_BYTE 0x32 0 // hair color
	//
	WRITE_SHORT 0x46 6 // natural AC
	WRITE_SHORT 0x48 6 // effective AC
	//
	WRITE_BYTE 0x53 3 // # attacks/round
	//
	WRITE_BYTE 0x45 10 // Hide in Shadows
	WRITE_BYTE 0x68 10 // Move Silently
	//
	WRITE_BYTE 0x237 IDS_OF_SYMBOL ("gender" "male") // sex
	// abilities
	WRITE_BYTE 0x238 14 // STR
	WRITE_BYTE 0x23A 4 // INT
	WRITE_BYTE 0x23B 12 // WIS
	WRITE_BYTE 0x23C 15 // DEX
	WRITE_BYTE 0x23D 13 // CON
	WRITE_BYTE 0x23E 6 // CHA
	//
	WRITE_BYTE 0x23F 10 // morale
	WRITE_BYTE 0x240 5 // morale break
	WRITE_SHORT 0x242 60 // morale recovery
	//
	WRITE_ASCII SCRIPT_OVERRIDE "GTACMP00" #8
	WRITE_ASCII SCRIPT_CLASS "%DEST_RES%" #8
	WRITE_ASCII SCRIPT_DEFAULT "GTACMPAI" #8
	// IDS
	WRITE_BYTE 0x270 IDS_OF_SYMBOL ("ea" "neutral")
	WRITE_BYTE 0x271 IDS_OF_SYMBOL ("general" "animal")
	WRITE_BYTE 0x272 IDS_OF_SYMBOL ("race" "cat")
	WRITE_BYTE 0x273 IDS_OF_SYMBOL ("class" "cat")
	WRITE_BYTE 0x275 IDS_OF_SYMBOL ("gender" "male")
	WRITE_BYTE 0x27B IDS_OF_SYMBOL ("align" "neutral")
	//
	WRITE_ASCII DEATHVAR "gtAnmlCompLeopard" #32
	// equipment
	ADD_CRE_ITEM "gtacp04a" #0 #0 #0 "identified&unstealable&undroppable" "weapon" EQUIP
	ADD_CRE_ITEM "gtacp04a" #0 #0 #0 "identified&unstealable&undroppable" "weapon"
	ADD_CRE_ITEM "gtacp04a" #0 #0 #0 "identified&unstealable&undroppable" "weapon"
	ADD_CRE_ITEM "gtacp04a" #0 #0 #0 "identified&unstealable&undroppable" "weapon"
	// passive traits
	LPF "ADD_CRE_EFFECT" INT_VAR "opcode" = 268 "target" = 1 "timing" = 1 END // clear fog of war
	LPF "ADD_CRE_EFFECT" INT_VAR "opcode" = 232 "target" = 1 "timing" = 1 "parameter2" = 16 "special" = BIT2 STR_VAR "resource" = "GTACMP00" END // cast spell onto myself upon death (suppress spell's name)
BUT_ONLY_IF_IT_CHANGES

/*
=====================================================================================================================
**ITM file(s)**
=====================================================================================================================
*/

CREATE "itm" "gtacp04a"
COPY_EXISTING "gtacp04a.itm" "override"
	/* Header */
	WRITE_LONG NAME1 "-1"
	WRITE_LONG NAME2 "-1"
	WRITE_LONG UNIDENTIFIED_DESC "-1"
	WRITE_LONG DESC "-1"
	WRITE_LONG 0x18 BIT5 // flags: not copyable (not needed...?)
	WRITE_SHORT 0x38 1 // maximum in stack
	WRITE_ASCII 0x3A "IWOLF" #8
	/* Extended Header */
	LPF ~ADD_ITEM_HEADER~ INT_VAR "type" = 1 "location" = 1 "range" = 1 "dicenumber" = 1 "dicesize" = 6 "damage_type" = 3 "depletion" = 1 "flags" = BIT0 "overhand" = 34 "backhand" = 33 "thrust" = 33 STR_VAR "icon" = "IWOLF" END // 1d6 (slashing)
	/* Feature Blocks */
	LPF "ADD_ITEM_EFFECT" INT_VAR "type" = 1 "target" = 1 "opcode" = 326 "timing" = 1 STR_VAR "resource" = "GTACP04A" END // Apply effects list
BUT_ONLY_IF_IT_CHANGES
//
ACTION_DEFINE_ASSOCIATIVE_ARRAY "cdtweaksAnimalCompanion" BEGIN
	// id => enchantment
	"b" => 0 // magical (+0)
	"c" => 1 // magical (+1)
	"d" => 2 // magical (+2)
	"e" => 3 // magical (+3)
END
ACTION_PHP_EACH "cdtweaksAnimalCompanion" AS "k" => "v" BEGIN
	COPY_EXISTING "gtacp04a.itm" "override\gtacp04%k%.itm"
		WRITE_LONG 0x18 (THIS BOR BIT6) // flags: magical
		WRITE_LONG 0x60 "%v%" // enchantment
		LPF "ALTER_ITEM_HEADER" INT_VAR "damage_bonus" = "%v%" "thac0_bonus" = "%v%" END
	BUT_ONLY_IF_IT_CHANGES
END

/*
=====================================================================================================================
**SPL file(s)**
=====================================================================================================================
*/

CREATE "spl" "gtacp04a"
COPY_EXISTING "gtacp04a.spl" "override"
	/* Header */
	WRITE_LONG NAME1 "-1"
	WRITE_LONG NAME2 "-1"
	WRITE_LONG UNIDENTIFIED_DESC "-1"
	WRITE_LONG DESC "-1"
	WRITE_LONG 0x18 BIT14 // Ignore dead/wild magic
	WRITE_SHORT 0x1C 4 // Type: Innate
	WRITE_LONG 0x34 1 // Level
	/* Extended Header */
	LPF ~ADD_SPELL_HEADER~ INT_VAR "range" = 30 END
	/* Feature Blocks */
	LPF "ADD_SPELL_EFFECT" INT_VAR "opcode" = 146 "target" = 2 "timing" = 1 "parameter2" = 1 STR_VAR "resource" = "GTACP04B" END // Cast spell (Cast instantly, ignore level)
BUT_ONLY
//
CREATE "spl" "gtacp04b"
COPY_EXISTING "gtacp04b.spl" "override"
	/* Header */
	WRITE_LONG NAME1 RESOLVE_STR_REF (@4001)
	WRITE_LONG NAME2 "-1"
	WRITE_LONG UNIDENTIFIED_DESC "-1"
	WRITE_LONG DESC "-1"
	WRITE_LONG 0x18 BIT14 // Ignore dead/wild magic
	WRITE_SHORT 0x1C 4 // Type: Innate
	WRITE_LONG 0x34 1 // Level
	/* Extended Header */
	LPF ~ADD_SPELL_HEADER~ INT_VAR "range" = 30 END
	/* Feature Blocks */
	LPF "ADD_SPELL_EFFECT" INT_VAR "opcode" = 324 "target" = 2 "parameter1" = IDS_OF_SYMBOL ("kit" "barbarian") "parameter2" = 109 STR_VAR "resource" = "%DEST_RES%" END // Protection from resource
	PATCH_WITH_SCOPE BEGIN
		LPF "ADD_SPLPROT_ENTRY" INT_VAR "stat" = IDS_OF_SYMBOL ("STATS" "IMMUNITY_TO_BACKSTAB") STR_VAR "value" = "-1" "relation" = "4" "label" = "STAT(IMMUNITY_TO_BACKSTAB) >= n" RET "index" END
		LPF "ADD_SPELL_EFFECT" INT_VAR "opcode" = 324 "target" = 2 "parameter1" = 1 "parameter2" = "%index%" STR_VAR "resource" = "%DEST_RES%" END // Protection from resource (IMMUNITY_TO_BACKSTAB >= 1)
	END
	LPF "ADD_SPELL_EFFECT" INT_VAR "opcode" = 324 "target" = 2 "parameter1" = IDS_OF_SYMBOL ("general" "plant") "parameter2" = 103 STR_VAR "resource" = "%DEST_RES%" END // Protection from resource
	LPF "ADD_SPELL_EFFECT" INT_VAR "opcode" = 324 "target" = 2 "parameter2" = 55 STR_VAR "resource" = "%DEST_RES%" END // Protection from resource (RACE=GOLEM || GENERAL=UNDEAD)
	LPF "ADD_SPELL_EFFECT" INT_VAR "opcode" = 324 "target" = 2 "parameter1" = IDS_OF_SYMBOL ("general" "plant") "parameter2" = 103 STR_VAR "resource" = "%DEST_RES%" END // Protection from resource
	LPF "ADD_SPELL_EFFECT" INT_VAR "opcode" = 324 "target" = 2 "parameter1" = IDS_OF_SYMBOL ("general" "weapon") "parameter2" = 103 STR_VAR "resource" = "%DEST_RES%" END // Protection from resource (Animated weapons such as the Mordenkainen's Sword)
	PATCH_WITH_SCOPE BEGIN
		PATCH_FOR_EACH "race" IN "mist" "dragon" "beholder" "slime" "demonic" "mephit" "imp" "elemental" "salamander" "genie" "solar" "antisolar" "planatar" "darkplanatar" BEGIN
			LPF "ADD_SPELL_EFFECT" INT_VAR "opcode" = 324 "target" = 2 "parameter1" = IDS_OF_SYMBOL ("race" "%race%") "parameter2" = 104 STR_VAR "resource" = "%DEST_RES%" END // Protection from resource
		END
	END
	LPF "ADD_SPELL_EFFECT" INT_VAR "opcode" = 402 "target" = 2 STR_VAR "resource" = "GTACP04B" END // Invoke lua
BUT_ONLY

/*
=====================================================================================================================
**LUA file(s)**
=====================================================================================================================
*/

COPY_EXISTING "m_gt#402.lua" "override"
	APPEND_FILE TEXT "cdtweaks\luke\lua\animal_companion\leopard\sneak_attack.lua"
BUT_ONLY UNLESS "^function GTACP04B"
//
ACTION_IF !(FILE_EXISTS_IN_GAME "m_gtlstn.lua") BEGIN
	COPY ".../cdtweaks-inlined/empty" "override\m_gtlstn.lua"
		DELETE_BYTES 0x0 BUFFER_LENGTH
		INSERT_BYTES 0x0 STRING_LENGTH "-- Listeners --%WNL%%WNL%"
		WRITE_ASCII 0x0 "-- Listeners --%WNL%%WNL%"
	BUT_ONLY_IF_IT_CHANGES
END
COPY_EXISTING "m_gtlstn.lua" "override"
	APPEND_FILE TEXT "cdtweaks\luke\lua\animal_companion\leopard\enable_disable_sneak_attack.lua"
	APPEND_FILE TEXT "cdtweaks\luke\lua\animal_companion\leopard\custom_actionbar.lua"
BUT_ONLY UNLESS "gtAnmlCompLeopardVisible"