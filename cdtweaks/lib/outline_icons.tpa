INCLUDE ~cdtweaks/lib/spell_ids_vars.tpa~

ACTION_DEFINE_ASSOCIATIVE_ARRAY cd_spell_bams BEGIN // mutual BAMs

  ~%CLERIC_DETECT_EVIL%~ => sppr104
  ~%CLERIC_FIND_TRAPS%~ => sppr205
  ~%CLERIC_DISPEL_MAGIC%~ => sppr303
  ~%CLERIC_INVISIBILITY_PURGE%~ => sppr309
  ~%CLERIC_ZONE_OF_SWEET_AIR%~ => sppr318
  ~%CLERIC_FAR_SIGHT%~ => sppr415
  ~%CLERIC_WONDROUS_RECALL%~ => sppr611
//  ~%CLERIC_SYMBOL_FEAR%~ => sppr706 // actually uses red icon from arcane symbol, fear
  ~%WIZARD_HOLD_PORTAL%~ => spwi109 // unused
  ~%WIZARD_IDENTIFY%~ => spwi110
  ~%WIZARD_INFRAVISION%~ => spwi111
  ~%WIZARD_DETECT_EVIL%~ => spwi202
  ~%WIZARD_DETECT_INVISIBILITY%~ => spwi203
  ~%WIZARD_KNOCK%~ => spwi207
  ~%WIZARD_KNOW_ALIGNMENT%~ => spwi208
  ~%WIZARD_CLAIRVOYANCE%~ => spwi301
  ~%WIZARD_TRUE_DISPEL_MAGIC%~ => spwi302
//  ~%WIZARD_HOLD_PERSON%~ => spwi306 // actually uses red icon from divine hold person
  ~%WIZARD_DETECT_ILLUSION%~ => spwi322
  ~%WIZARD_DISPEL_MAGIC%~ => spwi326
  ~%WIZARD_DIMENSION_DOOR%~ => spwi402
  ~%WIZARD_OTILUKES_RESILIENT_SPHERE%~ => spwi413
  ~%WIZARD_POLYMORPH_SELF%~ => spwi416
  ~%WIZARD_MINOR_SEQUENCER%~ => spwi420
  ~%WIZARD_TELEPORT_FIELD%~ => spwi421
  ~%WIZARD_FAR_SIGHT%~ => spwi424
  ~%WIZARD_EYE%~ => spwi425
  ~%WIZARD_SHADOW_DOOR%~ => spwi505
  ~%WIZARD_ORACLE%~ => spwi515
  ~%WIZARD_MISLEAD%~ => spwi607
  ~%WIZARD_TRUE_SIGHT%~ => spwi609 // also uused for divine true seeing
  ~%WIZARD_CONTINGENCY%~ => spwi617
  ~%WIZARD_PROJECT_IMAGE%~ => spwi703
  ~%WIZARD_SPELL_SEQUENCER%~ => spwi710
  ~%WIZARD_LIMITED_WISH%~ => spwi722
  ~%WIZARD_SIMULACRUM%~ => spwi804
  ~%WIZARD_SPELL_TRIGGER%~ => spwi809
  ~%WIZARD_CHAIN_CONTINGENCY%~ => spwi908
  ~%WIZARD_SHAPECHANGE%~ => spwi916
  ~%WIZARD_WISH%~ => spwi919

  // iwd-only spells, but keep here to catch iwdification et al
  ~%CLERIC_KNOW_ALIGNMENT%~ => sppr209
  ~%CLERIC_EXALTATION%~ => sppr321
  ~%WIZARD_CONTACT_OTHER_PLANE%~ => spwi528

END

ACTION_IF !game_is_iwdee BEGIN

  ACTION_DEFINE_ASSOCIATIVE_ARRAY cd_spell_bams BEGIN // spells with non-white icons ONLY in iwdee

    ~%CLERIC_STRENGTH_OF_ONE%~ => sppr312 // white in oiwd
    ~%CLERIC_REGENERATE%~ => sppr711 // not in oiwd
    ~%WIZARD_FRIENDS%~ => spwi107 // not in oiwd
    ~%WIZARD_FIND_FAMILAR%~ => spwi123 // not in oiwd
    ~%WIZARD_FOG_CLOUD%~ => spwi204 // unused
    ~%WIZARD_LUCK%~ => spwi209 // blue in oiwd
    ~%WIZARD_STRENGTH%~ => spwi214 // white in oiwd
    ~%WIZARD_WIZARD_LOCK%~ => spwi216 // unused
    ~%WIZARD_GLITTERDUST%~ => spwi224 // not in oiwd
    ~%WIZARD_HASTE%~ => spwi305 // white in oiwd
    ~%WIZARD_REMOVE_CURSE%~ => spwi410 // blue/red in oiwd
    ~%WIZARD_TENSERS_TRANSFORMATION%~ => spwi603 // white in oiwd
    ~%WIZARD_IMPROVED_HASTE%~ => spwi613 // not in oiwd

  END

END

ACTION_IF original_bg1 BEGIN

  ACTION_DEFINE_ASSOCIATIVE_ARRAY cd_spell_bams BEGIN // these spells are in iwdee, but don't use white icons there

    ~%WIZARD_DISPEL_MAGIC%~ => spwi302 // WIZARD_DISPEL_MAGIC is actually remove magic in bg2+

  END

END

ACTION_PHP_EACH cd_spell_bams AS spell => bam BEGIN

  OUTER_SPRINT replace ~none~

  COPY_EXISTING ~%spell%.spl~ ~override~
    READ_ASCII 0x3a replace
    BUT_ONLY IF_EXISTS

  ACTION_IF ("%replace%" STRING_COMPARE_CASE "none") BEGIN

    COPY ~cdtweaks/bam/%bam_outline%/%bam%c.bam~ ~override/%replace%.bam~
      PATCH_IF original_bg1 BEGIN // decompress for original bg
        READ_ASCII 0x0 sg (4) //Signature
        PATCH_IF (~%sg%~ STRING_EQUAL_CASE ~BAMC~ = 1) BEGIN
          READ_LONG 0x8 dl //Uncompressed data length
          DECOMPRESS_REPLACE_FILE 0xc (SOURCE_SIZE - 0xc) dl
        END
      END

  END

END

ACTION_FOR_EACH bam IN cdfaeric cddtgooc BEGIN // faerie fire/detect good from DR

  ACTION_IF FILE_EXISTS_IN_GAME ~%bam%.bam~ BEGIN

    COPY ~cdtweaks/bam/%bam_outline%/%bam%.bam~ ~override/%bam%.bam~
      PATCH_IF original_bg1 BEGIN // decompress for original bg
        READ_ASCII 0x0 sg (4) //Signature
        PATCH_IF (~%sg%~ STRING_EQUAL_CASE ~BAMC~ = 1) BEGIN
          READ_LONG 0x8 dl //Uncompressed data length
          DECOMPRESS_REPLACE_FILE 0xc (SOURCE_SIZE - 0xc) dl
        END
      END

  END

END
