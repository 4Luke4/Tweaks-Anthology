DEFINE_ACTION_FUNCTION "SPELLCRAFT_COUNTERSPELL"
BEGIN
	LAF "ADD_EXTENDED_STAT" STR_VAR "identifier" = "GT_IGNORE_ACTION_ADD_SPRITE_STARTED_ACTION_LISTENER" END
	//
	WITH_SCOPE BEGIN
		ACTION_CLEAR_ARRAY "cdtweaks_counterspell"
		ACTION_DEFINE_ASSOCIATIVE_ARRAY "cdtweaks_counterspell" BEGIN
			// mschool, casting sound => casting animation
			1, "CAS_M02" => 12 // ABJURER
			2, "CAS_M03" => 14 // CONJURER
			3, "CAS_M04" => 16 // DIVINER
			4, "CAS_M05" => 11 // ENCHANTER
			5, "CAS_M01" => 13 // ILLUSIONIST
			6, "CAS_M06" => 15 // INVOKER
			7, "CAS_M07" => 9 // NECROMANCER
			8, "CAS_M08" => 10 // TRANSMUTER
		END
		//
		ACTION_PHP_EACH "cdtweaks_counterspell" AS "key" => "value" BEGIN
			CREATE "spl" "cdctr#0%key%"
			COPY_EXISTING "cdctr#0%key%.spl" "override"
				WRITE_LONG NAME1 RESOLVE_STR_REF ((AT "%key%"))
				WRITE_LONG UNIDENTIFIED_DESC "-1"
				WRITE_LONG 0x18 (BIT9 BOR BIT10) // break sanctuary/invisibility
				WRITE_SHORT 0x1C 4 // type: innate
				WRITE_LONG 0x34 1 // level
				WRITE_SHORT 0x22 "%value%" // casting animation
				WRITE_BYTE 0x25 "%key%" // mschool
				WRITE_ASCII 0x10 "%key_1%" #8 // casting sound
				//
				LPF "ADD_SPELL_HEADER" INT_VAR "range" = 30 END
				//
				LPF "ADD_SPELL_EFFECT" INT_VAR "opcode" = 402 "target" = 2 "parameter1" = 3 STR_VAR "resource" = "GTCTRSPL" END // invoke lua
			BUT_ONLY
		END
	END
	//
	WITH_SCOPE BEGIN
		LAF "ADD_STATDESC_ENTRY" INT_VAR "description" = RESOLVE_STR_REF (@100) STR_VAR "bam_file" = "cdctricn" RET "feedback_icon" = "index" END
		//
		COPY "cdtweaks\luke\bam\spellcraft_counterspell\cdctricn.bam" "override"
		COPY "cdtweaks\luke\bam\spellcraft_counterspell\cdctrspl.bam" "override"
		//
		CREATE "spl" "cdctrsp1"
		COPY_EXISTING "cdctrsp1.spl" "override"
			WRITE_LONG NAME1 RESOLVE_STR_REF (@101)
			WRITE_LONG UNIDENTIFIED_DESC "-1"
			WRITE_LONG 0x18 (BIT9 BOR BIT10 BOR BIT14) // break sanctuary/invisibility, ignore dead/wild magic
			WRITE_SHORT 0x1C 4 // type: innate
			WRITE_LONG 0x34 1 // level
			WRITE_ASCII 0x3A "CDCTRSPL" #8 // icon
			//
			LPF "ADD_SPELL_HEADER" INT_VAR "target" = 5 "range" = 30 STR_VAR "icon" = "CDCTRSPL" END
			//
			LPF "ADD_SPELL_EFFECT" INT_VAR "opcode" = 321 "target" = 1 "timing" = 1 STR_VAR "resource" = "%DEST_RES%" END // remove effects by resource
			LPF "ADD_SPELL_EFFECT" INT_VAR "opcode" = 172 "target" = 1 "timing" = 1 STR_VAR "resource" = "%DEST_RES%" END // remove spell
			LPF "ADD_SPELL_EFFECT" INT_VAR "opcode" = 139 "target" = 1 "parameter1" = RESOLVE_STR_REF (@102) "timing" = 1 END // string
			LPF "ADD_SPELL_EFFECT" INT_VAR "opcode" = 142 "target" = 1 "parameter2" = "%feedback_icon%" "timing" = 1 END // icon
			LPF "ADD_SPELL_EFFECT" INT_VAR "opcode" = 402 "target" = 1 "parameter1" = 1 STR_VAR "resource" = "GTCTRSPL" END // invoke lua
		BUT_ONLY
		//
		CREATE "spl" "cdctrsp2"
		COPY_EXISTING "cdctrsp2.spl" "override"
			WRITE_LONG NAME1 RESOLVE_STR_REF (@103)
			WRITE_LONG UNIDENTIFIED_DESC "-1"
			WRITE_LONG 0x18 (BIT9 BOR BIT10 BOR BIT14) // break sanctuary/invisibility, ignore dead/wild magic
			WRITE_SHORT 0x1C 4 // type: innate
			WRITE_LONG 0x34 1 // level
			WRITE_ASCII 0x3A "CDCTRSPL" #8
			//
			LPF "ADD_SPELL_HEADER" INT_VAR "target" = 5 "range" = 30 STR_VAR "icon" = "CDCTRSPL" END
			//
			LPF "ADD_SPELL_EFFECT" INT_VAR "opcode" = 402 "target" = 1 "parameter1" = 2 STR_VAR "resource" = "GTCTRSPL" END // invoke lua
		BUT_ONLY
	END
	// lua
	WITH_SCOPE BEGIN
		OUTER_SET "feedback_strref_spellcraft" = RESOLVE_STR_REF (@200)
		OUTER_SET "feedback_strref_isCasting" = RESOLVE_STR_REF (@201)
		//
		LAF "APPEND_LUA_FUNCTION" STR_VAR "description" = "Spellcraft / Counterspell class feat for spellcasters" "sourceFileSpec" = "cdtweaks\luke\lua\spellcraft_counterspell\utility.lua" "destRes" = "m_gtctsp" END
		LAF "APPEND_LUA_FUNCTION" STR_VAR "description" = "Spellcraft / Counterspell class feat for spellcasters" "sourceFileSpec" = "cdtweaks\luke\lua\spellcraft_counterspell\gain_ability.lua" "destRes" = "m_gtctsp" END
		LAF "APPEND_LUA_FUNCTION" STR_VAR "description" = "Spellcraft / Counterspell class feat for spellcasters" "sourceFileSpec" = "cdtweaks\luke\lua\spellcraft_counterspell\main.lua" "destRes" = "m_gtctsp" END
		//
		LAF "APPEND_LUA_FUNCTION" STR_VAR "description" = "Utility Functions / Listeners" "sourceFileSpec" = "cdtweaks\luke\lua\utility\decode_spell.lua" "destRes" = "m_gtutil" END
		//
		ACTION_IF !(FILE_EXISTS_IN_GAME "m_gttbls.lua") BEGIN
			COPY "cdtweaks\luke\lua\m_gttbls.lua" "override"
		END
	END
END