/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Accelerate/decelerate romances                   \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

ACTION_IF game_includes_bg1 BEGIN

  ACTION_DEFINE_ASSOCIATIVE_ARRAY cd_romances_timers BEGIN
    //Game, bcs, base dlg, banter dlg, join dlg => var // Notes
    bg, ~dorn%eet_var%~,   ~dorn%eet_var%~,   ~bdorn%eet_var%~,   ~dornj%eet_var%~   => ~dorn_romance[12]~          // dorn bg
    bg, ~neera%eet_var%~,  ~neera%eet_var%~,  ~bneera%eet_var%~,  ~neeraj%eet_var%~  => ~neeraromancetimer~         // neera bg
    bg, ~rasaad%eet_var%~, ~rasaad%eet_var%~, ~brasaad%eet_var%~, ~rasaadj%eet_var%~ => ~rasaad_romance_timer[123]~ // rasaad bg

  END

END

ACTION_IF game_includes_soa BEGIN

  ACTION_DEFINE_ASSOCIATIVE_ARRAY cd_romances_timers BEGIN
    //Game, bcs, base dlg, banter dlg, join dlg => var // Notes
    bg2, aerie,   aerie,   baerie,  aeriej   => aerieromance         // aerie soa
    bg2, anomen,  anomen,  banomen, anomenj  => anomenromance        // anomen soa
    bg2, dorn,    dorn,    bdorn,   dornj    => dornlovetalkstimer   // dorn soa
    bg2, hexxat,  hexxat,  bhexxat, hexxatj  => hexxatlovetalkstimer // hexxat soa
    bg2, jaheira, jaheira, bjaheir, jaheiraj => jaheiraromance       // jaheira soa
    bg2, neera,   neera,   bneera,  neeraj   => neeralovetalkstimer  // neera soa
    bg2, rasaad,  rasaad,  brasaad, rasaadj  => rasaadlovetalkstimer // rasaad soa
    bg2, viconia, viconi,  bviconi, viconij  => viconiaromance       // viconia soa

  END

END

ACTION_IF game_includes_tob BEGIN

  ACTION_DEFINE_ASSOCIATIVE_ARRAY cd_romances_timers BEGIN
    //Game, bcs, base dlg, banter dlg, join dlg => var // Notes
    tob, aeri25,  aerie25a, baerie25, aerie25j => aerieromance            // aerie tob
    tob, anom25,  anome25a, banome25, anome25j => anomenromance           // anomen tob
    tob, dorn25,  dorn25a,  bdorn25,  dorn25j  => dorntoblovetalkstimer   // dorn tob
    tob, hexxa25, hexxa25a, bhexxa25, hexxa25j => hexxattoblovetalkstimer // hexxat tob
    tob, jahe25,  jahei25a, bjahei25, jahei25j => jaheiraromance          // jaheira tob
    tob, neer25,  neera25a, bneera25, neera25j => neeratoblovetalkstimer  // neera tob
    tob, rasa25,  rasaa25a, brasaa25, rasaa25j => rasaadtoblovetalkstimer // rasaad tob
    tob, vico25,  vicon25a, bvicon25, vicon25j => expviconiaromance       // viconia tob

  END

END

ACTION_PHP_EACH cd_romances_timers AS params => variable BEGIN

  ACTION_FOR_EACH file IN ~%params_1%.bcs~ ~%params_2%.dlg~ ~%params_3%.dlg~ ~%params_4%.dlg~ BEGIN

    ACTION_IF FILE_EXISTS_IN_GAME ~%file%~ THEN BEGIN

      COPY_EXISTING ~%file%~ ~override~
        DECOMPILE_AND_PATCH BEGIN
          REPLACE_EVALUATE CASE_INSENSITIVE ~\(RealSetGlobalTimer\|SetGlobalTimer\)("\(%variable%\)","\([^"]+\)",\([^)]+\))~ BEGIN
            PATCH_IF IS_AN_INT MATCH4 BEGIN
              SET time = MATCH4
            END ELSE BEGIN
              SET time = IDS_OF_SYMBOL (~GTIMES~ ~%MATCH4%~) // should convert stuff like EIGHT_HOURS to numeric value
            END
            SET time = ((time * romance_speed_factor) / 100)
          END ~%MATCH1%("%MATCH2%","%MATCH3%",%time%)~
        END
        BUT_ONLY

    END

  END

END
