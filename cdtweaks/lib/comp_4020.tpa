/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// ToB-Style NPCs                                   \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

MKDIR ~weidu_external/cdtweaks~

<<<<<<<<./inlined/tob_lookup.txt
>>>>>>>>

// load patch macro
INCLUDE ~cdtweaks/lib/tob_style_npcs.tpa~

INCLUDE ~cdtweaks/lib/joinable_npc_array.tpa~

ACTION_PHP_EACH JOINABLE_NPC_ARRAY AS file => dv BEGIN

  ACTION_IF !IS_AN_INT dv BEGIN // joinable NPC

    COPY_EXISTING ~%file%~ ~override~
      PATCH_IF (("%SOURCE_RES%" STRING_COMPARE_CASE "BDIMOEN") AND     // exclude special imoen files
                ("%SOURCE_RES%" STRING_COMPARE_CASE "BDSHIMOE") AND    // exclude special imoen files
                ("%SOURCE_RES%" STRING_COMPARE_CASE "FINIMOEN") AND    // exclude special imoen files
                ("%SOURCE_RES%" STRING_COMPARE_CASE "IMOENDRM")) BEGIN // exclude special imoen files
        READ_LONG 0x018 xp
        READ_BYTE 0x273 class
        READ_LONG 0x20 flags
        PATCH_IF (xp AND (class < 22) AND ((flags & BIT11) = 0)) BEGIN // sanity check: non-zero XP, player-available class, not flagged as dead
          SPRINT cre_file ~%SOURCE_RES%~
          INNER_ACTION BEGIN

            ACTION_IF !FILE_EXISTS ~weidu_external/cdtweaks/cdtob/%dv%_%class%.txt~ BEGIN
              COPY ~./inlined/tob_lookup.txt~ ~weidu_external/cdtweaks/cdtob/%dv%_%class%.txt~
            END
            APPEND_OUTER ~weidu_external/cdtweaks/cdtob/%dv%_%class%.txt~ ~%cre_file% %xp%~

          END
        END
      END
      BUT_ONLY

  END

END

ACTION_BASH_FOR ~weidu_external/cdtweaks/cdtob~ ~^.+_[0-9]+\.txt$~ BEGIN

  COPY ~%BASH_FOR_FILESPEC%~ ~%BASH_FOR_FILESPEC%~
    COUNT_2DA_ROWS 2 rows
    PATCH_IF (rows > 1) BEGIN // don't bother if only one creature
      READ_2DA_ENTRY 0 1 2 xp   // take first row as comparison point
      READ_2DA_ENTRY 0 0 2 orig
      FOR (index = 1 ; index < rows ; ++index) BEGIN
        READ_2DA_ENTRY index 1 2 xp_test
        PATCH_IF (xp_test < xp) BEGIN   // if lower XP creature, use it as comparison instead
          READ_2DA_ENTRY index 0 2 orig
          SET xp = xp_test
        END
      END
      FOR (index = 0 ; index < rows ; ++index) BEGIN
        READ_2DA_ENTRY index 0 2 dest
        INNER_ACTION BEGIN

          ACTION_IF (((FILE_EXISTS_IN_GAME ~%orig%.cre~) AND (FILE_EXISTS_IN_GAME ~%dest%.cre~)) AND // both exist
                      (~%orig%~ STRING_COMPARE_CASE ~%dest%~)) THEN BEGIN                            // skip self-copy

            COPY_EXISTING ~%orig%.cre~ ~override/%dest%.cre~
              PATCH_PRINT " -- copying %orig% to %dest%..."
              LAUNCH_PATCH_MACRO ~tob_style_npcs~
              BUT_ONLY

          END

        END
      END
    END
    BUT_ONLY

END
