/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                            \\\\\
///// Gradual Drow item disintegration                           \\\\\
/////                                                            \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

EXTEND_BOTTOM ~ar2500.bcs~ ~cdtweaks/baf/ar2500.baf~
EXTEND_BOTTOM ~baldur.bcs~ ~cdtweaks/baf/baldur.baf~

ACTION_IF ((FILE_EXISTS_IN_GAME ~baldur25.bcs~) AND NOT (game_is_eet)) THEN BEGIN // extend ToB game script if present

  EXTEND_BOTTOM ~baldur25.bcs~ ~cdtweaks/baf/baldur.baf~

END

ACTION_IF enhanced_edition BEGIN

  COPY_EXISTING ~ar4500.bcs~ ~override~
                ~ar6014.bcs~ ~override~
                ~ar6100.bcs~ ~override~
                ~ar6101.bcs~ ~override~
                ~ar6102.bcs~ ~override~
                ~ar6103.bcs~ ~override~
                ~ar6104.bcs~ ~override~
                ~ar6105.bcs~ ~override~
                ~ar6106.bcs~ ~override~
                ~ar6107.bcs~ ~override~
                ~ar6108.bcs~ ~override~
                ~ar6109.bcs~ ~override~
                ~ar6110.bcs~ ~override~
                ~ar6111.bcs~ ~override~ // ees use this check instead for sendai's enclave
    DECOMPILE_AND_PATCH BEGIN
      REPLACE_TEXTUALLY ~!Global("oh_drow_items_safe","GLOBAL",[01])~ ~False()~
    END
    BUT_ONLY IF_EXISTS

  EXTEND_BOTTOM ~baldur.bcs~   ~cdtweaks/baf/drow_ee.baf~
  EXTEND_BOTTOM ~baldur25.bcs~ ~cdtweaks/baf/drow_ee.baf~

END

OUTER_SET drow_msg = RESOLVE_STR_REF(@105001)

COPY ~cdtweaks/spl/cdbreak1.spl~ ~override~ // armor
  LPF ALTER_EFFECT INT_VAR match_opcode = 139 parameter1 = drow_msg END
  PATCH_IF enhanced_edition BEGIN // make non-PCs immune to the breakage (presumably they're drow, wielding weapons in drow areas)
    LPF ADD_SPELL_EFFECT INT_VAR insert_point = 0 opcode = 318 parameter1 = 2 parameter2 = 112 STR_VAR resource = ~cdbreak1~ END
  END

COPY_EXISTING ~cdbreak1.spl~ ~override/cdbreak4.spl~  // cloak
  LPF ALTER_EFFECT INT_VAR match_opcode = 143 parameter1 = 4 END
  PATCH_IF enhanced_edition BEGIN
    LPF ALTER_EFFECT INT_VAR match_opcode = 318 STR_VAR resource = EVAL ~%DEST_RES%~ END
  END

COPY_EXISTING ~cdbreak1.spl~ ~override/cdbreak9.spl~  // shield
  LPF ALTER_EFFECT INT_VAR match_opcode = 143 parameter1 = 9 END
  PATCH_IF enhanced_edition BEGIN
    LPF ALTER_EFFECT INT_VAR match_opcode = 318 STR_VAR resource = EVAL ~%DEST_RES%~ END
  END

ACTION_DEFINE_ASSOCIATIVE_ARRAY cd_drow_items BEGIN // items have chance of failing when used in combat
  dwblun01, 1 => cdblun01
  dwbolt01, 2 => cdbolt01
  dwbolt02, 2 => cdbolt02
  dwbolt03, 2 => cdbolt03
  dwchan01, 0 => cdbreak1
  dwchan02, 0 => cdbreak1
  dwclck01, 0 => cdbreak4
  dwhalb01, 1 => cdhalb01
  dwplat01, 0 => cdbreak1
  dwshld01, 0 => cdbreak9
  dwsper01, 1 => cdsper01
  dwsw1h01, 1 => cdsw1h01
  dwsw1h02, 1 => cdsw1h02
  dwxbow01, 4 => cdxbow01
  misc9w,   0 => cdbreak4
END

ACTION_PHP_EACH cd_drow_items AS params => spell BEGIN

  ACTION_IF params_1 != 0 BEGIN // weapon

    COPY_EXISTING ~%params_0%.itm~ ~override~
      LPF ADD_ITEM_EFFECT INT_VAR type = params_1 opcode = 146 target = 1 parameter1 = 1 timing = 1 probability1 = 0 STR_VAR resource = EVAL ~%spell%~ END
      BUT_ONLY IF_EXISTS

    COPY ~cdtweaks/spl/cdsw1h01.spl~ ~override/%spell%.spl~
      LPF ALTER_EFFECT INT_VAR match_opcode = 139 parameter1 = drow_msg END
      LPF ALTER_EFFECT INT_VAR match_opcode = 112 STR_VAR resource = EVAL ~%params_0%~ END
      PATCH_IF enhanced_edition BEGIN // make non-PCs immune to the breakage (presumably they're drow, wielding weapons in drow areas)
        LPF ADD_SPELL_EFFECT INT_VAR insert_point = 0 opcode = 318 parameter1 = 2 parameter2 = 112 STR_VAR resource = EVAL ~%spell%~ END
      END

  END ELSE BEGIN // armor

    COPY_EXISTING ~%params_0%.itm~ ~override~
      LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 232 target = 1 parameter1 = 1 timing = 2 STR_VAR resource = EVAL ~%spell%~ END
      BUT_ONLY IF_EXISTS

  END

END

ACTION_IF !enhanced_edition BEGIN // no fancy filtering for oBG2, so make drow creatures immune to breakage spells

  INCLUDE ~cdtweaks/lib/joinable_npc_array.tpa~

  ACTION_PHP_EACH JOINABLE_NPC_ARRAY AS file => dv BEGIN

    ACTION_IF IS_AN_INT dv BEGIN // non-joinable NPC

      COPY_EXISTING ~%file%~ ~override~ // make non-PCs immune to the breakage (presumably they're drow, wielding weapons in drow areas)
        CLEAR_ARRAY cd_drow_cre_patch
        READ_LONG 0x2bc item_off
        READ_LONG 0x2c0 item_num
        FOR (index = 0 ; index < item_num ; ++index) BEGIN
          READ_ASCII (item_off + (index * 0x14)) item
          PATCH_PHP_EACH cd_drow_items AS params => spell BEGIN
            PATCH_IF ("%item%" STRING_COMPARE_CASE "%params_0%" = 0) BEGIN
              DEFINE_ASSOCIATIVE_ARRAY cd_drow_cre_patch BEGIN ~%spell%~ => 0 END
            END
          END
        END
        PATCH_PHP_EACH cd_drow_cre_patch AS spell => foo BEGIN
// PATCH_PRINT ~ == adding protection to %spell% on %SOURCE_RES%~
          LPF ADD_CRE_EFFECT INT_VAR opcode = 206 target = 1 timing = 1 STR_VAR resource = EVAL ~%spell%~ END
        END
        BUT_ONLY

    END

  END

END
