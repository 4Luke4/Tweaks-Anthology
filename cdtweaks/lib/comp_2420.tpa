/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                            \\\\\
///// Loosen Equipment Restrictions for Cleric Multis            \\\\\
/////                                                            \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

// basically, only mess with kit flags if they still have their original usability and have not been hijacked by kit mods as free flags
OUTER_SET lathander = 0
OUTER_SET helm = 0
OUTER_SET talos = 0
ACTION_IF FILE_EXISTS_IN_GAME ~kitlist.2da~ BEGIN
  COPY_EXISTING ~kitlist.2da~ ~override~
    COUNT_2DA_ROWS ~9~ "rows"
    FOR ( index = 1 ; index < rows ; index = index + 1 ) BEGIN
      READ_2DA_ENTRY "%index%" 1 9 "kitname"
      READ_2DA_ENTRY "%index%" 7 9 "usability"
      //PATCH_PRINT ~on index %index%, kitname is %kitname% and usability is %usability%~
      PATCH_IF (("%kitname%" STRING_COMPARE_CASE "lathander" = 0) AND ("%usability%" STRING_COMPARE_CASE "0x04000000" = 0)) BEGIN
        SET lathander = 1
      END
      PATCH_IF (("%kitname%" STRING_COMPARE_CASE "helm" = 0) AND ("%usability%" STRING_COMPARE_CASE "0x02000000" = 0)) BEGIN
        SET helm = 1
      END
      PATCH_IF (("%kitname%" STRING_COMPARE_CASE "talos" = 0) AND ("%usability%" STRING_COMPARE_CASE "0x01000000" = 0)) BEGIN
        SET talos = 1
      END
    END
    BUT_ONLY

END

//Alignment changes
COPY_EXISTING_REGEXP GLOB ~^.+\.itm$~ ~override~
  READ_BYTE 0x1f "use2"
  READ_BYTE 0x20 "use3"
  SET patched = 0
  PATCH_IF (("%use2%" BAND 0b00001000) = 0b00000000) BEGIN // if usable by single-class fighters...
    SET "use2" = ("%use2%" BAND 0b00111111) // make usable by fc, fmc
    SET patched = 1
  END
  PATCH_IF (("%use3%" BAND 0b00000100) = 0b00000000) BEGIN // if usable by single-class mages...
    SET "use2" = ("%use2%" BAND 0b01111110) // make usable by mc, fmc
    SET patched = 1
  END
  PATCH_IF (("%use3%" BAND 0b01000000) = 0b00000000) BEGIN // if usable by single-class thieves...
    SET "use2" = ("%use2%" BAND 0b11111101) // make usable by ct
    SET patched = 1
  END
  PATCH_IF (("%use3%" BAND 0b00100000) = 0b00000000) BEGIN // if usable by single-class rangers...
    SET "use2" = ("%use2%" BAND 0b11111011) // make usable by cr
    SET patched = 1
  END
  WRITE_BYTE 0x1f "%use2%"
  WRITE_BYTE 0x20 "%use3%"
  PATCH_IF (patched AND (lathander OR helm OR talos)) BEGIN
    READ_BYTE 0x29 use4
    PATCH_IF lathander BEGIN
      SET use4 = (use4 BAND `BIT2)
    END
    PATCH_IF helm BEGIN
      SET use4 = (use4 BAND `BIT1)
    END
    PATCH_IF talos BEGIN
      SET use4 = (use4 BAND `BIT0)
    END
    WRITE_BYTE 0x29 use4
  END
  BUT_ONLY

COPY_EXISTING ~clasweap.2da~ ~override~
  PATCH_IF original_iwd BEGIN
    SET col_num = 16
  END ELSE BEGIN
    SET col_num = 9
  END
  READ_2DA_ENTRY 0 0 (col_num - 1) val                  // weidu seems to freak out with a top line w/ one less entry
  SET_2DA_ENTRY  0 0 (col_num - 1) ~CD_DELETE_ME %val%~ // temp
  FOR (col = 1; col < col_num; ++col) BEGIN
    READ_2DA_ENTRY  1 col col_num "mage"
    READ_2DA_ENTRY  2 col col_num "fighter"
    READ_2DA_ENTRY  4 col col_num "thief"
    READ_2DA_ENTRY  8 col col_num "ranger"
    PATCH_IF ("%fighter%" = 1) BEGIN
      SET_2DA_ENTRY 10 col col_num ~1~ // fc
    END
    PATCH_IF ("%mage%" = 1) BEGIN
      SET_2DA_ENTRY 14 col col_num ~1~ // cm
    END
    PATCH_IF ("%thief%" = 1) BEGIN
      SET_2DA_ENTRY 15 col col_num ~1~ // ct
    END
    PATCH_IF ("%ranger%" = 1) BEGIN
      SET_2DA_ENTRY 18 col col_num ~1~ // cr
    END
    PATCH_IF (("%fighter%" + "%mage%") > 0) BEGIN
      SET_2DA_ENTRY 17 col col_num ~1~ // fmc
    END
  END
  REPLACE_TEXTUALLY ~CD_DELETE_ME ~ ~~
  PRETTY_PRINT_2DA
  BUT_ONLY


ACTION_IF FILE_EXISTS_IN_GAME ~weapprof.2da~ THEN BEGIN

  ACTION_IF MOD_IS_INSTALLED ~cdtweaks/setup-cdtweaks.tp2~ ~2200~ THEN BEGIN // multiclass grandmastery

    OUTER_SET profmax = 5

  END ELSE BEGIN

    OUTER_SET profmax = 2

  END

  COPY_EXISTING ~weapprof.2da~  ~override~
    FOR (row = 9; row < 32; ++row) BEGIN
      READ_2DA_ENTRY row  4 22 mag // mage stars
      READ_2DA_ENTRY row  5 22 fgt // fighter stars
      READ_2DA_ENTRY row  7 22 thf // thief stars
      READ_2DA_ENTRY row 11 22 rgr // ranger stars
      READ_2DA_ENTRY row 13 22 fc  // fighter/cleric stars
      READ_2DA_ENTRY row 17 22 cm  // mage/cleric stars
      READ_2DA_ENTRY row 18 22 ct  // thief/cleric stars
      READ_2DA_ENTRY row 20 22 fmc // fighter/mage/cleric stars
      READ_2DA_ENTRY row 21 22 cr  // cleric/ranger stars
      PATCH_IF (fgt > profmax) BEGIN SET fgt = profmax END
      PATCH_IF (mag > profmax) BEGIN SET mag = profmax END
      PATCH_IF (thf > profmax) BEGIN SET thf = profmax END
      PATCH_IF (rgr > profmax) BEGIN SET rgr = profmax END
      PATCH_IF (fgt > fc) BEGIN
        SET_2DA_ENTRY row 13 22 ~%fgt%~
      END
      PATCH_IF (fgt > fmc) BEGIN
        SET_2DA_ENTRY row 20 22 ~%fgt%~
      END
      PATCH_IF (mag > cm) BEGIN
        SET_2DA_ENTRY row 17 22 ~%mag%~
      END
      PATCH_IF (thf > ct) BEGIN
        SET_2DA_ENTRY row 18 22 ~%thf%~
      END
      PATCH_IF (rgr > cr) BEGIN
        SET_2DA_ENTRY row 21 22 ~%rgr%~
      END
    END
    BUT_ONLY

END
