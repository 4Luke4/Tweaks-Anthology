DEFINE_ACTION_FUNCTION "PLANAR_TURNING"
BEGIN
	CREATE "spl" "cdplntrn"
	COPY_EXISTING "cdplntrn.spl" "override"
		WRITE_LONG NAME1 "-1"
		WRITE_LONG 0x18 BIT14 // flags: ignore dead/wild magic
		WRITE_SHORT 0x1C 4 // type: innate
		WRITE_LONG 0x34 1 // level
		//
		LPF "ADD_SPELL_HEADER" INT_VAR "target" = 5 "range" = 30 "projectile" = IDS_OF_SYMBOL ("projectl" "INAREANP") + 1 END
		//
		LPF "ADD_SPELL_EFFECT" INT_VAR "opcode" = 139 "target" = 1 "parameter1" = RESOLVE_STR_REF (@0) "timing" = 1 END // feedback message
		LPF "ADD_SPELL_EFFECT" INT_VAR "opcode" = 402 "target" = 2 STR_VAR "resource" = "GTPLNTRN" END // Invoke lua
	BUT_ONLY_IF_IT_CHANGES
	// Listener: run 'func' each time a sprite has finished evaluating its effects
	LAF "APPEND_LUA_FUNCTION" STR_VAR "description" = "Listeners" "sourceFileSpec" = "cdtweaks\luke\lua\planar_turning_modal.lua" "destRes" = "m_gtlstn" END
	//
	WITH_SCOPE BEGIN
		OUTER_SET "feedback_strref" = RESOLVE_STR_REF (@1)
		LAF "APPEND_LUA_FUNCTION" STR_VAR "description" = "Functions to be invoked via op402" "sourceFileSpec" = "cdtweaks\luke\lua\planar_turning.lua" "destRes" = "m_gt#402" END
	END
	//
	ACTION_IF !(FILE_EXISTS_IN_GAME "m_gttbls.lua") BEGIN
		COPY "cdtweaks\luke\lua\m_gttbls.lua" "override"
	END
END