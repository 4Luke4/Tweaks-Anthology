/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Protect Shar-Teel                                \\\\\
///// (SCS borrowing)                                  \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

ACTION_IF original_bg1 BEGIN // original bg - no min hp effect, so overcharge her HP and end fight when she's back to her normal HP

  ADD_SPELL ~cdtweaks/spl/cdshart.spl~ 3 1 CD_SHARTEEL_HP_REDUCTION

  COPY_EXISTING ~shartel2.bcs~ ~override~
    DECOMPILE_AND_PATCH BEGIN
      REPLACE_TEXTUALLY ~HPPercentLT(Myself,50)~ ~HPPercentLT(Myself,66)~
      REPLACE_TEXTUALLY ~HPPercentLT(Myself,75)~ ~HPPercentLT(Myself,90)~
    END

  <<<<<<<< ./inline/cdsharteel.baf
    IF
      InParty(Myself)
      Global("CD_SHARTEEL_HP_REDUCTION","GLOBAL",0)
    THEN
      RESPONSE #100
        ApplySpell(Myself,CD_SHARTEEL_HP_REDUCTION)
        SetGlobal("CD_SHARTEEL_HP_REDUCTION","GLOBAL",1)
    END
  >>>>>>>>

END ELSE BEGIN

  <<<<<<<< ./inline/cdsharteel.baf
    IF
      InParty(Myself)
      HasItem("monhp1",Myself)
    THEN
      RESPONSE #100
        DestroyItem("monhp1")
    END
  >>>>>>>>

END

INCLUDE ~cdtweaks/lib/joinable_npc_array.tpa~

ACTION_PHP_EACH JOINABLE_NPC_ARRAY AS file => dv BEGIN

  ACTION_IF ("%dv%" STRING_COMPARE_CASE "sharteel"  = 0) BEGIN

    COPY_EXISTING ~%file%~ ~override~
      PATCH_IF original_bg1 BEGIN
        WRITE_BYTE 0x24 ((THIS * 3) / 2) // increase by 50%
        WRITE_BYTE 0x26 ((THIS * 3) / 2) // increase by 50%
      END ELSE BEGIN
        ADD_CRE_ITEM monhp1 #0 #0 #0 none ~RRING LRING AMULET BELT BOOTS CLOAK GLOVES ARMOR HELMET~
      END
      
  END

END  

EXTEND_TOP ~%tutu_scripts%harteel.bcs~ ~./inline/cdsharteel.baf~
