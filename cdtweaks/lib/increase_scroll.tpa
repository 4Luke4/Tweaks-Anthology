ACTION_IF game_includes_pst THEN BEGIN

  ACTION_IF game_is_pst THEN BEGIN // pst but not pstee

    ACTION_IF stack > 255 BEGIN OUTER_SET stack = 255 END

  END

  OUTER_SPRINT scrollpattern ~^sp\(pr\|wi\)[0-9]+\.itm$~
END ELSE BEGIN
  OUTER_SPRINT scrollpattern ~^.+\.itm$~
END

ACTION_CLEAR_ARRAY cd_stackables

ACTION_IF (original_iwd OR original_bg1) THEN BEGIN // old engines need a header to be able to stack items
  OUTER_SET needs_header = 1
END ELSE BEGIN
  OUTER_SET needs_header = 0
END

PRINT @1
COPY_EXISTING_REGEXP GLOB ~%scrollpattern%~ ~override~ // looking for scrolls
  READ_SHORT  0x1c "type" ELSE 0
  PATCH_IF ("%type%" = 11) BEGIN // scrolls
    READ_SHORT 0x38 current
    PATCH_IF (stack > current) BEGIN
      WRITE_SHORT  0x38 stack
      DEFINE_ASSOCIATIVE_ARRAY cd_stackables BEGIN ~%SOURCE_RES%~ => ~%current%~ END
    END
    READ_SHORT 0x68 abil_num ELSE 1
    PATCH_IF ((needs_header = 1) AND (abil_num = 0)) BEGIN // old engines need a header to be able to stack items
      WRITE_SHORT  0x68 1 // one ability
      WRITE_LONG   0x6a (THIS + 0x38) // fx offset
      READ_LONG    0x64 abil_off
      INSERT_BYTES abil_off 0x38 // inserts new ability of type default (0)
    END
  END
  BUT_ONLY

INCLUDE ~cdtweaks/lib/increase_scripts.tpa~
