/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Change Magically Created Weapons to Zero Weight  \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

ACTION_IF enhanced_edition OR tutu_gen OR game_is_bgt OR original_bg2 OR game_is_iwd-in-bg2 OR game_is_ca BEGIN
  OUTER_SET check_255 = 1
END ELSE BEGIN
  OUTER_SET check_255 = 0
END

OUTER_SPRINT weight_temp @212001 // use existing weight string from arcane magic in heavy armor, but...
OUTER_INNER_PATCH_SAVE weight ~%weight_temp%~ BEGIN
  REPLACE_TEXTUALLY ~\[0-9\]\+~ ~~ // we'll remove actual numerical weight from match
END

ACTION_CLEAR_ARRAY  cd_magical_items
ACTION_DEFINE_ARRAY cd_magical_items BEGIN END

COPY_EXISTING_REGEXP GLOB ~^.+\.itm$~ ~override~
                          ~^.+\.spl$~ ~override~
  PATCH_IF ("%SOURCE_FILE%" STRING_COMPARE_REGEXP "^.+\.spl$" = 0) BEGIN
    SET "abil_length" = 0x28
  END ELSE BEGIN
    SET "abil_length" = 0x38
  END
  READ_LONG  0x64 abil_off ELSE 0
  READ_SHORT 0x68 abil_num ELSE 0
  READ_LONG  0x6a fx_off   ELSE 0
  FOR (index = 0 ; index < abil_num ; ++index) BEGIN
    READ_SHORT  (abil_off + 0x1e + (abil_length * index)) abil_fx_num
    READ_SHORT  (abil_off + 0x20 + (abil_length * index)) abil_fx_idx
    FOR (index2 = 0 ; index2 < abil_fx_num ; ++index2) BEGIN
      READ_SHORT (fx_off +        (0x30 * (abil_fx_idx + index2))) opcode
      PATCH_IF ((opcode = 111) OR ((opcode = 255) AND (check_255 = 1))) BEGIN // create magic item
        READ_ASCII (fx_off + 0x14 + (0x30 * (abil_fx_idx + index2))) item
        DEFINE_ASSOCIATIVE_ARRAY cd_magical_items BEGIN "%item%" => 0 END
      END
    END
  END
  BUT_ONLY

ACTION_PHP_EACH cd_magical_items AS item => foo BEGIN

  ACTION_IF FILE_EXISTS_IN_GAME ~%item%.itm~ THEN BEGIN

    COPY_EXISTING ~%item%.itm~ ~override~
      READ_LONG 0x4c weight_itm
      PATCH_IF (weight_itm != 0) BEGIN // don't bother if not non-zero
        WRITE_LONG 0x4c 0
        // adjust descriptions
        PATCH_FOR_EACH offset IN 0x50 0x54 BEGIN
          READ_STRREF ~%offset%~ desc
          PATCH_IF ("%desc%" STRING_COMPARE_REGEXP ~<Invalid Strref -?[0-9]+>~) BEGIN // checking for valid content in string
            INNER_PATCH_SAVE desc ~%desc%~ BEGIN
              REPLACE_TEXTUALLY ~%weight%[0-9]+~  ~\10~
            END
            SAY_EVALUATED ~%offset%~ ~%desc%~ // write changes
          END
        END
      END
      BUT_ONLY

  END

END
