/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Reveal City Areas                                \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

ACTION_IF game_includes_pst THEN BEGIN

  ACTION_FOR_EACH file IN 0100tear 0600malm ar0101 ar0300 ar0500 ar0700 ar1101 0200strk 1500gul9 ar0109 ar0400 ar0501 BEGIN

    EXTEND_TOP ~%file%.bcs~ ~cdtweaks/baf/a!explor.baf~

  END

END ELSE BEGIN 

  ACTION_CLEAR_ARRAY cd_reveal_maps

  ACTION_IF game_includes_iwd THEN BEGIN

    COPY_EXISTING ~ar1000.are~ ~override~ // easthaven
                  ~ar2100.are~ ~override~ // kuldahar
                  ~ar9100.are~ ~override~ // lonelywood
      READ_ASCII 0x94 script
      DEFINE_ASSOCIATIVE_ARRAY cd_reveal_maps BEGIN ~%script%~ => ~%SOURCE_RES%~ END 
      BUT_ONLY IF_EXISTS

  END

  ACTION_IF game_includes_bg1 THEN BEGIN

    COPY_EXISTING ~%NEBaldursGate_BCS%.bcs~ ~override~ // fix narlen script loop, if not already fixed
      DECOMPILE_AND_PATCH BEGIN
        REPLACE_TEXTUALLY ~TimeGT(20)~ ~TimeGT(20) Global("CDNarlenExists","GLOBAL",0)~
        REPLACE_TEXTUALLY ~TimeLT(4)~  ~TimeLT(4)  Global("CDNarlenExists","GLOBAL",0)~
        REPLACE_TEXTUALLY ~TimeGT(4)~  ~TimeGT(4)  Global("CDNarlenExists","GLOBAL",1)~
        REPLACE_TEXTUALLY ~\bActivate("narlen")~   ~Activate("narlen") SetGlobal("CDNarlenExists","GLOBAL",1)~
        REPLACE_TEXTUALLY ~Deactivate("narlen")~ ~Deactivate("narlen") SetGlobal("CDNarlenExists","GLOBAL",0)~
      END
      BUT_ONLY UNLESS ~CDNarlenExists~ IF_EXISTS

    COPY_EXISTING ~%NWBaldursGate%.are~      ~override~ // ar0100
                  ~%NBaldursGate%.are~       ~override~ // ar0200
                  ~%NEBaldursGate%.are~      ~override~ // ar0300
                  ~%WBaldursGate%.are~       ~override~ // ar0600
                  ~%CentralBaldursGate%.are~ ~override~ // ar0700
                  ~%EBaldursGate%.are~       ~override~ // ar0800
                  ~%UlgothsBeard%.are~       ~override~ // ar1000
                  ~%SWBaldursGate%.are~      ~override~ // ar1100
                  ~%BaldursGateDocks%.are~   ~override~ // ar1200
                  ~%SEBaldursGate%.are~      ~override~ // ar1300
                  ~%Candlekeep%.are~         ~override~ // ar2600
                  ~%Candlekeep_Ch6%.are~     ~override~ // ar2626
                  ~%Beregost%.are~           ~override~ // ar3300
                  ~%Nashkel%.are~            ~override~ // ar4800
      READ_ASCII 0x94 script
      DEFINE_ASSOCIATIVE_ARRAY cd_reveal_maps BEGIN ~%script%~ => ~%SOURCE_RES%~ END 
      BUT_ONLY IF_EXISTS

  END
  
  ACTION_PHP_EACH cd_reveal_maps AS script => scope BEGIN 
  
    EXTEND_BOTTOM ~%script%.bcs~ ~cdtweaks/baf/reveal.baf~ EVALUATE_BUFFER
      
  END    
  
END 
