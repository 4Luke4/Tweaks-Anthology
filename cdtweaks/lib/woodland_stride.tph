DEFINE_ACTION_FUNCTION "WOODLAND_STRIDE"
BEGIN
	LAF "GT_ADD_SPELL"
	INT_VAR
		"level" = 6
		"type" = 4
		"preferredSlot" = 43
	STR_VAR
		"idsName" = "DRUID_WOODLAND_STRIDE"
	RET
		"DRUID_WOODLAND_STRIDE" = "resName"
	END
	//
	LAF "ADD_EXTENDED_STAT" STR_VAR "identifier" = "GT_IMMUNITIES_VIA_403" END
	//
	WITH_SCOPE BEGIN
		ACTION_TO_LOWER "DRUID_WOODLAND_STRIDE"
		COPY "cdtweaks\luke\bam\class\woodland_stride.bam" "override\%DRUID_WOODLAND_STRIDE%d.bam" // courtesy of zenblack
	END
	//
	WITH_SCOPE BEGIN
		LAF "ADD_STATDESC_ENTRY" INT_VAR "description" = RESOLVE_STR_REF (@0) STR_VAR "bam_file" = "%DRUID_WOODLAND_STRIDE%D" RET "feedback_icon" = "index" END
		// Listener(s)
		LAF "APPEND_LUA_FUNCTION" STR_VAR "description" = "AI-related stuff" "sourceFileSpec" = "cdtweaks\luke\lua\ai\object_type.lua" "destRes" = "m_gt#ai" END
		LAF "APPEND_LUA_FUNCTION" STR_VAR "description" = "Class/Kit Abilities" "sourceFileSpec" = "cdtweaks\luke\lua\class\woodland_stride.lua" "destRes" = "m_gtspcl" END
		LAF "APPEND_LUA_FUNCTION" STR_VAR "description" = "Lua Tools" "sourceFileSpec" = "cdtweaks\luke\lua\tool\array_contains.lua" "destRes" = "m_gttool" END
		// immunities via 403
		LAM "READ_SPELL_IDS"
		LAF "APPEND_LUA_FUNCTION" STR_VAR "description" = "Immunities via 403 / Removal via 402" "sourceFileSpec" = "cdtweaks\luke\lua\immunity_removal.lua" "destRes" = "m_gtimrm" END
		//
		ACTION_IF !(FILE_EXISTS_IN_GAME "m_gttbls.lua") BEGIN
			COPY "cdtweaks\luke\lua\m_gttbls.lua" "override"
		END
	END
	//
	WITH_SCOPE BEGIN
		<<<<<<<< .../cdtweaks-inlined/gt_clear_aux.baf
		IF
			True()
		THEN
			RESPONSE #100
				EEex_LuaAction("GT_ImmunitiesVia403_ClearAux()")
				Continue()
		END
		>>>>>>>>
		//
		ACTION_FOR_EACH "world_script" IN "baldur" "baldur25" "bdbaldur" BEGIN
			ACTION_IF (FILE_EXISTS_IN_GAME "%world_script%.bcs") BEGIN
				EXTEND_TOP "%world_script%.bcs" ".../cdtweaks-inlined/gt_clear_aux.baf"
			END
		END
	END
END