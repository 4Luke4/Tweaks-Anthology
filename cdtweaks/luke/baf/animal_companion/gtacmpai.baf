/* Initialize */

IF
	OR(2)
		StateCheck(Myself,STATE_IMMOBILE)
		StateCheck(Myself,STATE_REALLY_DEAD)
THEN
	RESPONSE #100
		NoAction()
END

IF
	!Allegiance(Myself,EVILCUTOFF)
	!InActiveArea(Myself)
	!Range(NearestEnemyOf(Myself),30)
THEN
	RESPONSE #100
		NoAction()
END

/* When panicked, wander continuously (without pausing) */

IF
	StateCheck(Myself,STATE_PANIC)
THEN
	RESPONSE #100
		RandomWalkContinuous()
END

/* When blinded, consider the idea of wandering a bit in the hope of finding someone to attack */

IF
	!GlobalTimerNotExpired("gt_BlindTimer","LOCALS")
	EEex_LuaTrigger("return GT_AI_DoNotOverridePlayerCommands()")
	StateCheck(Myself,STATE_BLIND)
	!Range(NearestEnemyOf(Myself),2)
THEN
	RESPONSE #100
		SetGlobalTimer("gt_BlindTimer","LOCALS",ONE_ROUND)
		Continue()
END

IF
	GlobalTimerNotExpired("gt_BlindTimer","LOCALS")
	ActionListEmpty()
	ActuallyInCombat()
	StateCheck(Myself,STATE_BLIND)
	!Range(NearestEnemyOf(Myself),2)
THEN
	RESPONSE #100
		EEex_LuaAction("GT_AI_EquipMostDamagingMelee()")
		RandomWalkContinuous()
		Continue()
	RESPONSE #100
		EEex_LuaAction("GT_AI_EquipMostDamagingMelee()")
		SetSequence(SEQ_ATTACK)
		Continue()
	RESPONSE #100
		EEex_LuaAction("GT_AI_EquipMostDamagingMelee()")
		RandomTurn()
		Continue()
	RESPONSE #100
		EEex_LuaAction("GT_AI_EquipMostDamagingMelee()")
		Continue()
END

/* When STATE_INVISIBLE and sufficiently intelligent, consider the idea of wandering a bit to confuse position */

IF
	!GlobalTimerNotExpired("gt_RandomWalkTimer","LOCALS")
	EEex_LuaTrigger("return GT_AI_DoNotOverridePlayerCommands()")
	CheckStatGT(Myself,7,INT)
	StateCheck(Myself,STATE_INVISIBLE)
	!StateCheck(Myself,STATE_BLIND)
	Detect(NearestEnemyOf(Myself))
THEN
	RESPONSE #100
		SetGlobalTimerRandom("gt_RandomWalkTimer","LOCALS",ONE_ROUND,TWO_ROUNDS)
		SetGlobal("gt_RandomWalk","LOCALS",1)
		Continue()
END

IF
	GlobalTimerExpired("gt_RandomWalkTimer","LOCALS")
	EEex_LuaTrigger("return GT_AI_DoNotOverridePlayerCommands()")
	CheckStatGT(Myself,7,INT)
	StateCheck(Myself,STATE_INVISIBLE)
	!StateCheck(Myself,STATE_BLIND)
	!Detect(NearestEnemyOf(Myself))
THEN
	RESPONSE #100
		SetGlobal("gt_RandomWalk","LOCALS",2)
		Continue()
END

IF
	GlobalTimerNotExpired("gt_RandomWalkTimer","LOCALS")
	Global("gt_RandomWalk","LOCALS",1)
	CheckStatGT(Myself,7,INT)
	ActionListEmpty()
	ActuallyInCombat()
	StateCheck(Myself,STATE_INVISIBLE)
	!StateCheck(Myself,STATE_BLIND)
	Detect(NearestEnemyOf(Myself))
THEN
	RESPONSE #100
		RandomWalkContinuous()
		Continue()
	RESPONSE #100
		Continue()
END

IF
	GlobalTimerExpired("gt_RandomWalkTimer","LOCALS")
	Global("gt_RandomWalk","LOCALS",2)
	CheckStatGT(Myself,7,INT)
	ActionListEmpty()
	ActuallyInCombat()
	StateCheck(Myself,STATE_INVISIBLE)
	!StateCheck(Myself,STATE_BLIND)
	!Detect(NearestEnemyOf(Myself))
THEN
	RESPONSE #100
		MoveToObject(LastSeenBy(Myself))
		Continue()
END

/* Force "AttackReevaluate()" to terminate after one round */

IF
	Global("gt_ValidTarget","LOCALS",1)
	GlobalTimerExpired("gt_AttackTimer","LOCALS") // set it to one round (... or set it to a random amount of time between 1 and 2 rounds...?)
	EEex_LuaTrigger("return GT_AI_DoNotOverridePlayerCommands()")
THEN
	RESPONSE #100
		SetGlobal("gt_ValidTarget","LOCALS",0)
		Continue()
END

//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\

// Special Abilities blocks \\

//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\

// Frost Breath (winter wolf) \\
IF
	!GlobalTimerNotExpired("gt_cast","LOCALS")
	ActionListEmpty()
	HaveSpellRES("GTACMP01")
	!ButtonDisabled(BUTTON_INNATEBUTTON)
	EEex_LuaTrigger("return GT_AI_CastSpell({ ['resref'] = 'GTACMP01', ['opcode'] = {12}, ['stats'] = {{'RESISTCOLD','<',60}}, ['targetIDS'] = {'[<EA_PLACEHOLDER>.0.<HATEDRACE_PLACEHOLDER>]'}, ['EA'] = 'sourceAndTargetEnemies' })")
THEN
	RESPONSE #100
		SetGlobalTimer("gt_cast","LOCALS",ONE_ROUND)
		EEex_SpellObjectOffsetRES("GTACMP02",EEex_Target("gt_target"),[-1.-1])
	RESPONSE #100
		SetGlobalTimer("gt_cast","LOCALS",ONE_ROUND)
		EEex_SpellObjectOffsetRES("GTACMP02",EEex_Target("gt_target"),[-1.1])
	RESPONSE #100
		SetGlobalTimer("gt_cast","LOCALS",ONE_ROUND)
		EEex_SpellObjectOffsetRES("GTACMP02",EEex_Target("gt_target"),[1.-1])
	RESPONSE #100
		SetGlobalTimer("gt_cast","LOCALS",ONE_ROUND)
		EEex_SpellObjectOffsetRES("GTACMP02",EEex_Target("gt_target"),[1.1])
END

// Spittle (spitting snake) \\
IF
	!GlobalTimerNotExpired("gt_cast","LOCALS")
	ActionListEmpty()
	HaveSpellRES("GTACMP06")
	!ButtonDisabled(BUTTON_INNATEBUTTON)
	EEex_LuaTrigger("return GT_AI_CastSpell({ ['resref'] = 'GTACMP06', ['opcode'] = {74}, ['targetIDS'] = {'[<EA_PLACEHOLDER>.0.<HATEDRACE_PLACEHOLDER>]'}, ['EA'] = 'sourceAndTargetEnemies' })")
THEN
	RESPONSE #100
		SetGlobalTimer("gt_cast","LOCALS",ONE_ROUND)
		SpellRES("GTACMP06",EEex_Target("gt_target"))
	RESPONSE #50
		Continue()
END

//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\

// Attack blocks \\

//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\

IF
	ActionListEmpty()
	CheckStatGT(Myself,7,INT)
	!IsWeaponRanged(Myself)
	EEex_LuaTrigger("return GT_AI_Attack({ ['targetIDS'] = {'[<EA_PLACEHOLDER>.0.<HATEDRACE_PLACEHOLDER>]'} })")
	Range(EEex_Target("gt_target"),4)
	StateCheck(EEex_Target("gt_target"),0x100029)
THEN
	RESPONSE #100
		SetGlobal("gt_ValidTarget","LOCALS",1)
		SetGlobalTimer("gt_AttackTimer","LOCALS",6)
		AttackReevaluate(EEex_Target("gt_target"),15)
	RESPONSE #100
		SetGlobal("gt_ValidTarget","LOCALS",1)
		SetGlobalTimer("gt_AttackTimer","LOCALS",6)
		AttackReevaluate(EEex_Target("gt_target"),30)
	RESPONSE #100
		SetGlobal("gt_ValidTarget","LOCALS",1)
		SetGlobalTimer("gt_AttackTimer","LOCALS",6)
		AttackReevaluate(EEex_Target("gt_target"),45)
	RESPONSE #100
		SetGlobal("gt_ValidTarget","LOCALS",1)
		SetGlobalTimer("gt_AttackTimer","LOCALS",6)
		AttackReevaluate(EEex_Target("gt_target"),60)
END

IF
	ActionListEmpty()
	CheckStatGT(Myself,7,INT)
	EEex_LuaTrigger("return GT_AI_Attack({ ['targetIDS'] = {'[<EA_PLACEHOLDER>.0.<HATEDRACE_PLACEHOLDER>]'}, ['stats'] = {{'STONESKINS','==',0}}, ['state'] = {{'!','STATE_MIRRORIMAGE'}} })")
THEN
	RESPONSE #100
		SetGlobal("gt_ValidTarget","LOCALS",1)
		SetGlobalTimer("gt_AttackTimer","LOCALS",6)
		AttackReevaluate(EEex_Target("gt_target"),15)
	RESPONSE #100
		SetGlobal("gt_ValidTarget","LOCALS",1)
		SetGlobalTimer("gt_AttackTimer","LOCALS",6)
		AttackReevaluate(EEex_Target("gt_target"),30)
	RESPONSE #100
		SetGlobal("gt_ValidTarget","LOCALS",1)
		SetGlobalTimer("gt_AttackTimer","LOCALS",6)
		AttackReevaluate(EEex_Target("gt_target"),45)
	RESPONSE #100
		SetGlobal("gt_ValidTarget","LOCALS",1)
		SetGlobalTimer("gt_AttackTimer","LOCALS",6)
		AttackReevaluate(EEex_Target("gt_target"),60)
END

IF
	ActionListEmpty()
	CheckStatGT(Myself,4,INT)
	EEex_LuaTrigger("return GT_AI_Attack({ ['targetIDS'] = {'[<EA_PLACEHOLDER>.0.<HATEDRACE_PLACEHOLDER>]'}, ['stats'] = {{'STONESKINS','==',0}} })")
THEN
	RESPONSE #100
		SetGlobal("gt_ValidTarget","LOCALS",1)
		SetGlobalTimer("gt_AttackTimer","LOCALS",6)
		AttackReevaluate(EEex_Target("gt_target"),15)
	RESPONSE #100
		SetGlobal("gt_ValidTarget","LOCALS",1)
		SetGlobalTimer("gt_AttackTimer","LOCALS",6)
		AttackReevaluate(EEex_Target("gt_target"),30)
	RESPONSE #100
		SetGlobal("gt_ValidTarget","LOCALS",1)
		SetGlobalTimer("gt_AttackTimer","LOCALS",6)
		AttackReevaluate(EEex_Target("gt_target"),45)
	RESPONSE #100
		SetGlobal("gt_ValidTarget","LOCALS",1)
		SetGlobalTimer("gt_AttackTimer","LOCALS",6)
		AttackReevaluate(EEex_Target("gt_target"),60)
END

IF
	ActionListEmpty()
	CheckStatGT(Myself,1,INT)
	EEex_LuaTrigger("return GT_AI_Attack({ ['targetIDS'] = {'[<EA_PLACEHOLDER>.0.<HATEDRACE_PLACEHOLDER>]'} })")
THEN
	RESPONSE #100
		SetGlobal("gt_ValidTarget","LOCALS",1)
		SetGlobalTimer("gt_AttackTimer","LOCALS",6)
		AttackReevaluate(EEex_Target("gt_target"),15)
	RESPONSE #100
		SetGlobal("gt_ValidTarget","LOCALS",1)
		SetGlobalTimer("gt_AttackTimer","LOCALS",6)
		AttackReevaluate(EEex_Target("gt_target"),30)
	RESPONSE #100
		SetGlobal("gt_ValidTarget","LOCALS",1)
		SetGlobalTimer("gt_AttackTimer","LOCALS",6)
		AttackReevaluate(EEex_Target("gt_target"),45)
	RESPONSE #100
		SetGlobal("gt_ValidTarget","LOCALS",1)
		SetGlobalTimer("gt_AttackTimer","LOCALS",6)
		AttackReevaluate(EEex_Target("gt_target"),60)
END

// Default \\

IF
	ActionListEmpty()
	See(NearestEnemyOf(Myself))
	OR(2)
		!Allegiance(Myself,GOODCUTOFF)
		!InParty(NearestEnemyOf(Myself))
	!StateCheck(NearestEnemyOf(Myself),STATE_REALLY_DEAD)
THEN
	RESPONSE #100
		SetGlobal("gt_ValidTarget","LOCALS",1)
		SetGlobalTimer("gt_AttackTimer","LOCALS",6)
		EEex_LuaAction("GT_AI_EquipMostDamagingMelee()")
		AttackReevaluate(NearestEnemyOf(Myself),15)
	RESPONSE #100
		SetGlobal("gt_ValidTarget","LOCALS",1)
		SetGlobalTimer("gt_AttackTimer","LOCALS",6)
		EEex_LuaAction("GT_AI_EquipMostDamagingMelee()")
		AttackReevaluate(NearestEnemyOf(Myself),30)
	RESPONSE #100
		SetGlobal("gt_ValidTarget","LOCALS",1)
		SetGlobalTimer("gt_AttackTimer","LOCALS",6)
		EEex_LuaAction("GT_AI_EquipMostDamagingMelee()")
		AttackReevaluate(NearestEnemyOf(Myself),45)
	RESPONSE #100
		SetGlobal("gt_ValidTarget","LOCALS",1)
		SetGlobalTimer("gt_AttackTimer","LOCALS",6)
		EEex_LuaAction("GT_AI_EquipMostDamagingMelee()")
		AttackReevaluate(NearestEnemyOf(Myself),60)
END

